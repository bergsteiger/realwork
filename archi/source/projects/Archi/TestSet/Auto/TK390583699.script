USES
	ArchiControls.script
	QuickTestsUtils.script
;

CONST ArchiTestIni 'ArchiTest.ini'
CONST "Путь к копии базы" 'C:\Base\testbase2'
CONST "Все dll" '*.dll'

STRING FUNCTION "Путь Архивариуса"
	application:ExeName sysutils:ExtractFilePath >>> Result
;

STRING FUNCTION "Путь к копии Архивариуса" STRING IN aSourceDir
	STRING VAR l_SubDir
	script:FileName '' sysutils:ChangeFileExt sysutils:ExtractFileName >>> l_SubDir 
	[ aSourceDir l_SubDir ] strings:Cat >>> Result	
;

PROCEDURE "Подготовка к копированию" STRING IN aDestDir
	"Создать каталог, если он не существует {(aDestDir)}"
	"Очистить каталог {(aDestDir)}"
;

PROCEDURE "Распаковка тестовой базы"
	"Закрыть все окна"
	"Обработать сообщения" 
	'testbase.zip' "Путь к копии базы" ClearDataBase 
;

PROCEDURE "Очистка после теста"
	"Почистить за собой"
	"Очистить каталог {('C:\Base\testbase2')}"
;

PROCEDURE "Копировать в каталог для теста dll" STRING IN aDestDir STRING IN aSourceDir
	"Все dll" "Не создавать копию перезаписываемого файла" aDestDir aSourceDir CopyFilesByMask
;

PROCEDURE "Копировать в каталог для теста ArchiTest" STRING IN aFile STRING IN aDestDir STRING IN aSourceDir 
	aFile "Не создавать копию перезаписываемого файла" aDestDir aSourceDir CopyFile
;

STRING FUNCTION "Путь к директории TestSet в CVS"
	'' test:ResolveInputFilePath >>> Result	
;

PROCEDURE "Копировать в каталог для теста файл" STRING IN aDestDir STRING IN aFile
	STRING VAR l_ArchiIni_CVS_Path
	"Путь к директории TestSet в CVS" >>> l_ArchiIni_CVS_Path
	[ l_ArchiIni_CVS_Path 'CMD\' aFile ] strings:Cat >>> l_ArchiIni_CVS_Path
	STRING VAR l_DestDirArchiTestIni
	[ aDestDir ArchiTestIni ] strings:Cat >>> l_DestDirArchiTestIni
	"Не создавать копию перезаписываемого файла" l_DestDirArchiTestIni l_ArchiIni_CVS_Path CopyFile
;

PROCEDURE "Копирование исполняемых файлов" STRING IN aDestDir STRING IN aSourceDir
	"Копировать в каталог для теста dll {(aDestDir)} {(aSourceDir)}"
	"Копировать в каталог {(aDestDir)} для теста файл {('ArchiTest.ini')}"
	"Копировать в каталог {(aDestDir)} для теста файл {('OneTest.txt')}"
	"Копировать в каталог для теста ArchiTest {('ArchiTest.exe')} {(aDestDir)} {(aSourceDir)}"
	"Копировать в каталог для теста ArchiTest {('ArchiTest.map')} {(aDestDir)} {(aSourceDir)}"
;

PROCEDURE "Запуск второй копии тестового Архивариуса" STRING IN aDestDir
 	STRING VAR l_CMDLINE
	[ 'start ""' aDestDir 'ArchiTest.exe' '-toK -FakeK -File:OneTest.txt'] strings:Cat >>> l_CMDLINE
	l_CMDLINE WinExec
;

Тест TK390583699
	Параметры: ( 
		"Не открывать документ" 
	)
	Выполнить ( 

		STRING VAR l_SourceDir
           	"Путь Архивариуса" >>> l_SourceDir
          	STRING VAR l_DestDir 
           	"Путь к копии Архивариуса {(l_SourceDir)}" >>> l_DestDir
		"Подготовка к копированию {(l_DestDir)}"
		"Копирование исполняемых файлов {(l_DestDir)} {(l_SourceDir)}"
		"Распаковка тестовой базы"

		"Запуск второй копии тестового Архивариуса {(l_DestDir)}"

		// "Проверка результатов запуска" // Может быть понадобиться объединить с предыдущей.

                "Очистка после теста"
	)
;

TK390583699