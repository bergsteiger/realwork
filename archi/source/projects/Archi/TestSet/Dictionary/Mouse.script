// Mouse.script
// Вспомогательные методы для работы с мыши

USES
 axiom:Mouse
 axiom:Control
 axiom:Editor
 axiom:DDSupport
	ArchiSystem.script
;

WordAlias "Узнать индекс курсора мыши" mouse:GetCursorIndex

WordAlias "Узнать имя индекса курсора мыши" mouse:GetCursorNameByIndex

PROCEDURE "Прокрутить колесо мыши вниз для контрола" OBJECT IN aControl
 aControl pop:Control:MouseWheelDown
 ProcessMessages
;

PROCEDURE "Прокрутить колесо мыши вверх для контрола" OBJECT IN aControl
 aControl pop:Control:MouseWheelUp
 ProcessMessages
;

PROCEDURE "Зажать левую кнопку мыши"
  Mouse:LeftDown
  ProcessMessages
;

PROCEDURE "Отпустить левую кнопку мыши"
  Mouse:LeftUp
  ProcessMessages
;

PROCEDURE "Зажать правую кнопку мыши"
  Mouse:RightDown
  ProcessMessages
;

PROCEDURE "Отпустить правую кнопку мыши"
  Mouse:RightUp
  ProcessMessages
;

PROCEDURE "Зажать среднюю кнопку мыши"
  Mouse:MiddleDown
  ProcessMessages
;

PROCEDURE "Отпустить среднюю кнопку мыши"
  Mouse:MiddleUp
  ProcessMessages
;

PROCEDURE "Клик левой кнопкой мыши"
  Mouse:LeftDown
  Mouse:LeftUp
  ProcessMessages
;

PROCEDURE "Клик правой кнопкой мыши"
  Mouse:RightDown
  Mouse:RightUp
  ProcessMessages
;

PROCEDURE "Клик средней кнопкой мыши"
  Mouse:MiddleDown
  Mouse:MiddleUp
  ProcessMessages
;

PROCEDURE "Двойной клик"
 "Клик левой кнопкой мыши"
 OnTest
 "Клик левой кнопкой мыши"
 OnTest
;

PROCEDURE "Установить курсор мыши по координатам" INTEGER IN X INTEGER IN Y
 X Y mouse:SetCursorPosition
 "Обработать сообщения"
;

PROCEDURE "Установить курсор мыши посередине редактора" OBJECT IN anEditor
  INTEGER VAR x
  INTEGER VAR y
  anEditor pop:editor:ParaMiddleCoordsToScreen >>> y >>> x
  y 6 + >>> y
  x y mouse:SetCursorPosition
  100 SLEEP
  ProcessMessages
;

PROCEDURE "Установить курсор мыши посередине текущего редактора"
 "Убедиться, что фокус в редакторе"
 "Установить курсор мыши посередине редактора {(focused:control:push)}"
;

PROCEDURE "Установить курсор мыши на левый край редактора" OBJECT IN anEditor
  INTEGER VAR x
  INTEGER VAR y
  anEditor pop:editor:ParaCoordsToScreen >>> y >>> x
  y 6 + >>> y
  x y mouse:SetCursorPosition
  100 SLEEP
  ProcessMessages
;

PROCEDURE "Установить курсор мыши на левый край текущего редактора"
 "Убедиться, что фокус в редакторе"
 "Установить курсор мыши на левый край редактора {(focused:control:push)}"
;

PROCEDURE "Установить курсор мыши на правый край редактора" OBJECT IN anEditor
  INTEGER VAR x
  INTEGER VAR y
  anEditor pop:editor:ParaEndCoordsToScreen >>> y >>> x
  y 6 - >>> y
  x y mouse:SetCursorPosition
  100 SLEEP
  ProcessMessages
;

PROCEDURE "Установить курсор мыши на правый край текущего редактора"
 "Убедиться, что фокус в редакторе"
 "Установить курсор мыши на правый край редактора {(focused:control:push)}"
;

PROCEDURE "Установить курсор мыши чуть правее левого края редактора" OBJECT IN anEditor
  INTEGER VAR x
  INTEGER VAR y
  anEditor pop:editor:ParaCoordsToScreen >>> y >>> x
  x 6 + >>> x
  y 6 + >>> y
  x y mouse:SetCursorPosition
  100 SLEEP
  ProcessMessages
;

PROCEDURE "Установить курсор мыши чуть правее левого края текущего редактора"
 "Убедиться, что фокус в редакторе"
 "Установить курсор мыши чуть правее левого края редактора {(focused:control:push)}"
;

PROCEDURE "Установить курсор мыши чуть левее левого края редактора" OBJECT IN anEditor
  INTEGER VAR x
  INTEGER VAR y
  anEditor pop:editor:ParaCoordsToScreen >>> y >>> x
  x 6 - >>> x
  y 6 + >>> y
  x y mouse:SetCursorPosition
  100 SLEEP
  ProcessMessages
;

PROCEDURE "Установить курсор мыши чуть левее левого края текущего редактора"
 "Убедиться, что фокус в редакторе"
 "Установить курсор мыши чуть левее левого края редактора {(focused:control:push)}"
;

PROCEDURE "Установить курсор мыши чуть левее правого края редактора" OBJECT IN anEditor
  INTEGER VAR x
  INTEGER VAR y
  anEditor pop:editor:ParaEndCoordsToScreen >>> y >>> x
  x 6 - >>> x
  y 6 - >>> y
  x y mouse:SetCursorPosition
  100 SLEEP
  ProcessMessages
;

PROCEDURE "Установить курсор мыши чуть левее правого края текущего редактора"
 "Убедиться, что фокус в редакторе"
 "Установить курсор мыши чуть левее правого края редактора {(focused:control:push)}"
;

PROCEDURE "Установить курсор мыши чуть правее правого края редактора" OBJECT IN anEditor
  INTEGER VAR x
  INTEGER VAR y
  anEditor pop:editor:ParaEndCoordsToScreen >>> y >>> x
  x 6 + >>> x
  y 6 - >>> y
  x y mouse:SetCursorPosition
  100 SLEEP
  ProcessMessages
;

PROCEDURE "Установить курсор мыши чуть правее правого края текущего редактора"
 "Убедиться, что фокус в редакторе"
 "Установить курсор мыши чуть правее правого края редактора {(focused:control:push)}"
;

PROCEDURE "Ставим указатель мыши на конец текущего параграфа редактора со смещением" OBJECT IN aEditor INTEGER IN aX INTEGER IN aY
  INTEGER VAR x
  INTEGER VAR y
  aEditor pop:editor:ParaEndCoordsToScreen >>> y >>> x
  aX =+ x
  aY =+ y
  x y mouse:SetCursorPosition
  OnTest
;

PROCEDURE "Проверить, что курсор над ссылкой"
  'курсор над ссылкой:' . "Узнать индекс курсора мыши" "Узнать имя индекса курсора мыши" .
;

PROCEDURE "Проверить, что курсор вне ссылки"
  'курсор вне ссылки:' . "Узнать индекс курсора мыши" "Узнать имя индекса курсора мыши" .
;

PROCEDURE "Поставить курсор мыши на ссылку на графическую копию в текущем редакторе"
 VAR Editor
 INTEGER VAR x
 INTEGER VAR y
 "Стрелка вверх"
 focused:control:push >>> Editor
 Editor pop:editor:ParaCoordsToScreen >>> y >>> x
 x 15 + >>> x
 y 15 - >>> y
 x y mouse:SetCursorPosition
 ProcessMessages
; // "Поставить курсор мыши на ссылку на графическую копию в текущем редакторе"

PROCEDURE "Прокрутить колесо мыши вниз раз для контрола" INTEGER IN aCount OBJECT IN aControl
 aCount раз ( "Прокрутить колесо мыши вниз для контрола {(aControl)}" )
;
WordAlias "Прокрутить колесо мыши вниз раза для контрола" "Прокрутить колесо мыши вниз раз для контрола"

PROCEDURE "Прокрутить колесо мыши вверх раз для контрола" INTEGER IN aCount OBJECT IN aControl
 aCount раз ( "Прокрутить колесо мыши вверх для контрола {(aControl)}" )
;
WordAlias "Прокрутить колесо мыши вверх раза для контрола" "Прокрутить колесо мыши вверх раз для контрола"

PROCEDURE "Прокрутить колесом мыши вниз" OBJECT IN anEditor
	false true anEditor pop:editor:WheelScroll
;

PROCEDURE "Прокрутить колесом мыши вверх" OBJECT IN anEditor
	true true anEditor pop:editor:WheelScroll
;

PROCEDURE "Прокрутить колесом мыши вправо" OBJECT IN anEditor
	false false anEditor pop:editor:WheelScroll
;

PROCEDURE "Прокрутить колесом мыши влево" OBJECT IN anEditor
	true false anEditor pop:editor:WheelScroll
;

WordAlias "Запомнить позицию мыши" mouse:GetCursorPosition

WordAlias "Восстановить позицию мыши" mouse:SetCursorPosition

PROCEDURE "Запомнить позицию мыши и выполнить" IN aProc
 INTEGER VAR x_coord
 INTEGER VAR y_coord
 "Запомнить позицию мыши" >>> y_coord >>> x_coord
 TRY
  aProc DO
 FINALLY
  x_coord y_coord mouse:SetCursorPosition 
 END
;

PROCEDURE "Провести мышью вниз на пикселей" INTEGER IN aCount
 INTEGER VAR x
 INTEGER VAR y
 "Запомнить позицию мыши" >>> y >>> x
  aCount  2 / раз (
  1 SLEEP
  2 LOOP OnTest
  y 2 + >>> y
  x y mouse:SetCursorPosition 
 )
; // "Провести мышью вниз на пикселей"

PROCEDURE "Текущий индекс курсора мыши ?" STRING IN aIndex_Mouse
 Если ( "Узнать индекс курсора мыши" "Узнать имя индекса курсора мыши" РАВНО aIndex_Mouse ) то
   ( [ 'Курсор мыши: ' aIndex_Mouse '.' ] strings:Cat . )
  иначе
   ( 'Курсор мыши отличается от заданного: ' "Узнать индекс курсора мыши" "Узнать имя индекса курсора мыши"  Cat . )
; // "Текущий индекс курсора мыши ?"

: "Убедиться, что текущий индекс курсора мыши и выполнить"  STRING IN aIndex_Mouse IN aProc
 "Узнать индекс курсора мыши" "Узнать имя индекса курсора мыши" РАВНО aIndex_Mouse 'Курсор мыши отличается от указанного! Видимо не попали на ссылку!' ASSERTS
 aProc DO
; // "Убедиться, что текущий индекс курсора мыши и выполнить"

PROCEDURE "Установить курсор мыши по координатам относительно контрола" INTEGER IN X INTEGER IN Y OBJECT IN aControl
 X Y aControl pop:Control:ClientToScreen mouse:SetCursorPosition
 "Обработать сообщения"
;

PROCEDURE "Установить курсор мыши по координатам относительно контрола в фокусе" INTEGER IN X INTEGER IN Y
	"Установить курсор мыши по координатам {( X Y )} относительно контрола {(focused:control:push)}"
;

PROCEDURE "Убедиться, что операция Drag&Drop не выполняется"
	DDSupport:GetState РАВНО dsPassive 'Выполняется операция Drag&Drop, которой не должно быть!' ASSERTS 
;

INTEGER FUNCTION "Координата X текущего параграфа"
	0 [ "Контрол в фокусе" pop:editor:ParaCoordsToScreen ] [i] >>> Result
;

INTEGER FUNCTION "Координата Y текущего параграфа"
	1 [ "Контрол в фокусе" pop:editor:ParaCoordsToScreen ] [i] >>> Result 
;

PROCEDURE "Установить курсор мыши по координатам относительно параграфа"  INTEGER IN aX  INTEGER IN aY
	INTEGER VAR X
	INTEGER VAR Y
	"Координата X текущего параграфа" >>> X
	"Координата Y текущего параграфа" >>> Y
	aX +! X
	aY +! Y
	"Установить курсор мыши по координатам {(X)} {(Y)}"
	500 SLEEP
	"Обработать сообщения"
;
