UNIT core.ms.dict

USES
 axiom_push.ms.dict
 macro.ms.dict
 types.ms.dict
 Documentation.ms.dict
;

STRING FUNCTION NameOf
 ^ LINK IN aWord
 aWord |N >>> Result
; // NameOf

MACRO RecurseProc
// - процедура, которая умеет вызываться рекурсивно
 ^L IN aName
 axiom:PushSymbol FORWARD
 aName |N Ctx:Parser:PushSymbol

 axiom:PushSymbol PROCEDURE
 aName |N Ctx:Parser:PushSymbol
; // RecurseProc

: |^
// - разыменовывает параметр слева
 ^@ IN aRef
 aRef pop:Word:GetRef DO
; // |^

FUNCTOR : |@
// - получаем ссылку из параметра слева
 ^@ IN aRef
 aRef pop:Word:GetRef >>> Result
; // |@

MACRO this.do
 %SUMMARY '- запускаем "себя же" рекурсивно' ;
 axiom:PushSymbol (
 axiom:PushSymbol @SELF
 axiom:PushSymbol DO
 axiom:PushSymbol )
; // this.do

WordAlias call.me this.do

WordAlias !== <>
 %REMARK 'Оператор НЕ РАВНО. Двусторонний, а не ОПЗ (https://ru.wikipedia.org/wiki/%D0%9E%D0%B1%D1%80%D0%B0%D1%82%D0%BD%D0%B0%D1%8F_%D0%BF%D0%BE%D0%BB%D1%8C%D1%81%D0%BA%D0%B0%D1%8F_%D0%B7%D0%B0%D0%BF%D0%B8%D1%81%D1%8C). Может сравнивать значения РАЗНЫХ типов.'
WordAlias =/= !==
 %REMARK 'Оператор НЕ РАВНО. Двусторонний, а не ОПЗ. Может сравнивать значения РАЗНЫХ типов.'

BOOLEAN FUNCTION IsWordDeclared
 STRING IN aName
 %SUMMARY 'Определяет, что слово aName определено в данном месте' ;
 aName Ctx:PushFinder pop:KeywordFinder:KeywordByName <> nil >>> Result
; // IsWordDeclared

STRING FUNCTION NameAsString
 STRING IN aName
 %SUMMARY 'Делает имя таким, чтобы оно было как для Ctx:Parser:PushString'; 
 [ '`' aName '`' ] strings:Cat >>> Result
; // NameAsString
