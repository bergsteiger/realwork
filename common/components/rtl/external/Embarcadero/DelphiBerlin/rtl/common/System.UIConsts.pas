{*******************************************************}
{                                                       }
{           CodeGear Delphi Runtime Library             }
{                                                       }
{ Copyright(c) 2016 Embarcadero Technologies, Inc.      }
{              All rights reserved                      }
{                                                       }
{*******************************************************}

unit System.UIConsts;

interface

uses System.UITypes, System.Classes;

const
  claAliceblue = TAlphaColors.AliceBlue;
  claAntiquewhite = TAlphaColors.Antiquewhite;
  claAqua = TAlphaColors.Aqua;
  claAquamarine = TAlphaColors.Aquamarine;
  claAzure = TAlphaColors.Azure;
  claBeige = TAlphaColors.Beige;
  claBisque = TAlphaColors.Bisque;
  claBlack = TAlphaColors.Black;
  claBlanchedalmond = TAlphaColors.Blanchedalmond;
  claBlue = TAlphaColors.Blue;
  claBlueviolet = TAlphaColors.Blueviolet;
  claBrown = TAlphaColors.Brown;
  claBurlywood = TAlphaColors.Burlywood;
  claCadetblue = TAlphaColors.Cadetblue;
  claChartreuse = TAlphaColors.Chartreuse;
  claChocolate = TAlphaColors.Chocolate;
  claCoral = TAlphaColors.Coral;
  claCornflowerblue = TAlphaColors.Cornflowerblue;
  claCornsilk = TAlphaColors.Cornsilk;
  claCrimson = TAlphaColors.Crimson;
  claCyan = TAlphaColors.Cyan;
  claDarkblue = TAlphaColors.Darkblue;
  claDarkcyan = TAlphaColors.Darkcyan;
  claDarkgoldenrod = TAlphaColors.Darkgoldenrod;
  claDarkgray = TAlphaColors.Darkgray;
  claDarkgreen = TAlphaColors.Darkgreen;
  claDarkgrey = TAlphaColors.Darkgrey;
  claDarkkhaki = TAlphaColors.Darkkhaki;
  claDarkmagenta = TAlphaColors.Darkmagenta;
  claDarkolivegreen = TAlphaColors.Darkolivegreen;
  claDarkorange = TAlphaColors.Darkorange;
  claDarkorchid = TAlphaColors.Darkorchid;
  claDarkred = TAlphaColors.Darkred;
  claDarksalmon = TAlphaColors.Darksalmon;
  claDarkseagreen = TAlphaColors.Darkseagreen;
  claDarkslateblue = TAlphaColors.Darkslateblue;
  claDarkslategray = TAlphaColors.Darkslategray;
  claDarkslategrey = TAlphaColors.Darkslategrey;
  claDarkturquoise = TAlphaColors.Darkturquoise;
  claDarkviolet = TAlphaColors.Darkviolet;
  claDeeppink = TAlphaColors.Deeppink;
  claDeepskyblue = TAlphaColors.Deepskyblue;
  claDimgray = TAlphaColors.Dimgray;
  claDimgrey = TAlphaColors.Dimgrey;
  claDodgerblue = TAlphaColors.Dodgerblue;
  claFirebrick = TAlphaColors.Firebrick;
  claFloralwhite = TAlphaColors.Floralwhite;
  claForestgreen = TAlphaColors.Forestgreen;
  claFuchsia = TAlphaColors.Fuchsia;
  claGainsboro = TAlphaColors.Gainsboro;
  claGhostwhite = TAlphaColors.Ghostwhite;
  claGold = TAlphaColors.Gold;
  claGoldenrod = TAlphaColors.Goldenrod;
  claGray = TAlphaColors.Gray;
  claGreen = TAlphaColors.Green;
  claGreenyellow = TAlphaColors.Greenyellow;
  claGrey = TAlphaColors.Grey;
  claHoneydew = TAlphaColors.Honeydew;
  claHotpink = TAlphaColors.Hotpink;
  claIndianred = TAlphaColors.Indianred;
  claIndigo = TAlphaColors.Indigo;
  claIvory = TAlphaColors.Ivory;
  claKhaki = TAlphaColors.Khaki;
  claLavender = TAlphaColors.Lavender;
  claLavenderblush = TAlphaColors.Lavenderblush;
  claLawngreen = TAlphaColors.Lawngreen;
  claLemonchiffon = TAlphaColors.Lemonchiffon;
  claLightblue = TAlphaColors.Lightblue;
  claLightcoral = TAlphaColors.Lightcoral;
  claLightcyan = TAlphaColors.Lightcyan;
  claLightgoldenrodyellow = TAlphaColors.Lightgoldenrodyellow;
  claLightgray = TAlphaColors.Lightgray;
  claLightgreen = TAlphaColors.Lightgreen;
  claLightgrey = TAlphaColors.Lightgrey;
  claLightpink = TAlphaColors.Lightpink;
  claLightsalmon = TAlphaColors.Lightsalmon;
  claLightseagreen = TAlphaColors.Lightseagreen;
  claLightskyblue = TAlphaColors.Lightskyblue;
  claLightslategray = TAlphaColors.Lightslategray;
  claLightslategrey = TAlphaColors.Lightslategrey;
  claLightsteelblue = TAlphaColors.Lightsteelblue;
  claLightyellow = TAlphaColors.Lightyellow;
  claLime = TAlphaColors.Lime;
  claLimegreen = TAlphaColors.Limegreen;
  claLinen = TAlphaColors.Linen;
  claMagenta = TAlphaColors.Magenta;
  claMaroon = TAlphaColors.Maroon;
  claMediumaquamarine = TAlphaColors.Mediumaquamarine;
  claMediumblue = TAlphaColors.Mediumblue;
  claMediumorchid = TAlphaColors.Mediumorchid;
  claMediumpurple = TAlphaColors.Mediumpurple;
  claMediumseagreen = TAlphaColors.Mediumseagreen;
  claMediumslateblue = TAlphaColors.Mediumslateblue;
  claMediumspringgreen = TAlphaColors.Mediumspringgreen;
  claMediumturquoise = TAlphaColors.Mediumturquoise;
  claMediumvioletred = TAlphaColors.Mediumvioletred;
  claMidnightblue = TAlphaColors.Midnightblue;
  claMintcream = TAlphaColors.Mintcream;
  claMistyrose = TAlphaColors.Mistyrose;
  claMoccasin = TAlphaColors.Moccasin;
  claNavajowhite = TAlphaColors.Navajowhite;
  claNavy = TAlphaColors.Navy;
  claOldlace = TAlphaColors.Oldlace;
  claOlive = TAlphaColors.Olive;
  claOlivedrab = TAlphaColors.Olivedrab;
  claOrange = TAlphaColors.Orange;
  claOrangered = TAlphaColors.Orangered;
  claOrchid = TAlphaColors.Orchid;
  claPalegoldenrod = TAlphaColors.Palegoldenrod;
  claPalegreen = TAlphaColors.Palegreen;
  claPaleturquoise = TAlphaColors.Paleturquoise;
  claPalevioletred = TAlphaColors.Palevioletred;
  claPapayawhip = TAlphaColors.Papayawhip;
  claPeachpuff = TAlphaColors.Peachpuff;
  claPeru = TAlphaColors.Peru;
  claPink = TAlphaColors.Pink;
  claPlum = TAlphaColors.Plum;
  claPowderblue = TAlphaColors.Powderblue;
  claPurple = TAlphaColors.Purple;
  claRed = TAlphaColors.Red;
  claRosybrown = TAlphaColors.Rosybrown;
  claRoyalblue = TAlphaColors.Royalblue;
  claSaddlebrown = TAlphaColors.Saddlebrown;
  claSalmon = TAlphaColors.Salmon;
  claSandybrown = TAlphaColors.Sandybrown;
  claSeagreen = TAlphaColors.Seagreen;
  claSeashell = TAlphaColors.Seashell;
  claSienna = TAlphaColors.Sienna;
  claSilver = TAlphaColors.Silver;
  claSkyblue = TAlphaColors.Skyblue;
  claSlateblue = TAlphaColors.Slateblue;
  claSlategray = TAlphaColors.Slategray;
  claSlategrey = TAlphaColors.Slategrey;
  claSnow = TAlphaColors.Snow;
  claSpringgreen = TAlphaColors.Springgreen;
  claSteelblue = TAlphaColors.Steelblue;
  claTan = TAlphaColors.Tan;
  claTeal = TAlphaColors.Teal;
  claThistle = TAlphaColors.Thistle;
  claTomato = TAlphaColors.Tomato;
  claTurquoise = TAlphaColors.Turquoise;
  claViolet = TAlphaColors.Violet;
  claWheat = TAlphaColors.Wheat;
  claWhite = TAlphaColors.White;
  claWhitesmoke = TAlphaColors.Whitesmoke;
  claYellow = TAlphaColors.Yellow;
  claYellowgreen = TAlphaColors.Yellowgreen;
  claNull = TAlphaColors.Null;

{ Cursor string functions }

function CursorToString(Cursor: TCursor): string;
function StringToCursor(const S: string): TCursor;
procedure GetCursorValues(const Proc: TGetStrProc);
function CursorToIdent(Cursor: Integer; var Ident: string): Boolean;
function IdentToCursor(const Ident: string; var Cursor: Integer): Boolean;
procedure RegisterCursorIntegerConsts;

{ TColor string functions }

function ColorToString(Color: TColor): string;
function StringToColor(const S: string): TColor;
procedure GetColorValues(Proc: TGetStrProc);
function ColorToIdent(Color: Integer; var Ident: string): Boolean;
function IdentToColor(const Ident: string; var Color: Integer): Boolean;
procedure RegisterColorIntegerConsts;

{ TAlphaColor string functions }

procedure GetAlphaColorValues(Proc: TGetStrProc);
function AlphaColorToString(Value: TAlphaColor): string;
function StringToAlphaColor(const Value: string): TAlphaColor;
function AlphaColorToIdent(Color: Integer; var Ident: string): Boolean;
function IdentToAlphaColor(const Ident: string; var Color: Integer): Boolean;
procedure RegisterAlphaColorIntegerConsts;

{ TAlphaColor function's }

/// <summary>Converts TAlphaColor into TColor structure, exchanging red and blue channels while losing alpha channel. </summary>
function AlphaColorToColor(const Color: TAlphaColor): TColor;
function AppendColor(Start, Stop: TAlphaColor): TAlphaColor;
function SubtractColor(Start, Stop: TAlphaColor): TAlphaColor;
function RGBtoBGR(const C: TAlphaColor): TAlphaColor;
function CorrectColor(const C: TAlphaColor): TAlphaColor;
function PremultiplyAlpha(const C: TAlphaColor): TAlphaColor;
function UnpremultiplyAlpha(const C: TAlphaColor): TAlphaColor;
function MakeColor(R, G, B: Byte; A: Byte = $FF): TAlphaColor; overload;
function MakeColor(const C: TAlphaColor; const AOpacity: Single): TAlphaColor; overload;
function HSLtoRGB(H, S, L: Single): TAlphaColor;
procedure RGBtoHSL(RGB: TAlphaColor; out H, S, L: Single);
function ChangeHSL(const C: TAlphaColor; dH, dS, dL: Single): TAlphaColor;

implementation

uses System.SysUtils;

{ Cursor translation function }

const
  DeadCursors = 1;

const
  Cursors: array[0..21] of TIdentMapEntry = (
    (Value: crDefault;      Name: 'crDefault'),
    (Value: crArrow;        Name: 'crArrow'),
    (Value: crCross;        Name: 'crCross'),
    (Value: crIBeam;        Name: 'crIBeam'),
    (Value: crSizeNESW;     Name: 'crSizeNESW'),
    (Value: crSizeNS;       Name: 'crSizeNS'),
    (Value: crSizeNWSE;     Name: 'crSizeNWSE'),
    (Value: crSizeWE;       Name: 'crSizeWE'),
    (Value: crUpArrow;      Name: 'crUpArrow'),
    (Value: crHourGlass;    Name: 'crHourGlass'),
    (Value: crDrag;         Name: 'crDrag'),
    (Value: crNoDrop;       Name: 'crNoDrop'),
    (Value: crHSplit;       Name: 'crHSplit'),
    (Value: crVSplit;       Name: 'crVSplit'),
    (Value: crMultiDrag;    Name: 'crMultiDrag'),
    (Value: crSQLWait;      Name: 'crSQLWait'),
    (Value: crNo;           Name: 'crNo'),
    (Value: crAppStart;     Name: 'crAppStart'),
    (Value: crHelp;         Name: 'crHelp'),
    (Value: crHandPoint;    Name: 'crHandPoint'),
    (Value: crSizeAll;      Name: 'crSizeAll'),

    { Dead cursors }
    (Value: crSize;         Name: 'crSize'));

function CursorToString(Cursor: TCursor): string;
begin
  if not CursorToIdent(Cursor, Result) then
    FmtStr(Result, '%d', [Cursor]);
end;

function StringToCursor(const S: string): TCursor;
var
  L: Integer;
begin
  if not IdentToCursor(S, L) then L := StrToInt(S);
  Result := L;
end;

procedure GetCursorValues(const Proc: TGetStrProc);
var
  I: Integer;
begin
  for I := Low(Cursors) to High(Cursors) - DeadCursors do Proc(Cursors[I].Name);
end;

function CursorToIdent(Cursor: Integer; var Ident: string): Boolean;
begin
  Result := IntToIdent(Cursor, Ident, Cursors);
end;

function IdentToCursor(const Ident: string; var Cursor: Integer): Boolean;
begin
  Result := IdentToInt(Ident, Cursor, Cursors);
end;

procedure RegisterCursorIntegerConsts;
begin
  if not Assigned(FindIntToIdent(TypeInfo(TCursor))) then
    RegisterIntegerConsts(TypeInfo(TCursor), IdentToCursor, CursorToIdent);
end;

{ Color mapping routines }

const
  Colors: array[0..51] of TIdentMapEntry = (
    (Value: TColors.Black; Name: 'clBlack'),
    (Value: TColors.Maroon; Name: 'clMaroon'),
    (Value: TColors.Green; Name: 'clGreen'),
    (Value: TColors.Olive; Name: 'clOlive'),
    (Value: TColors.Navy; Name: 'clNavy'),
    (Value: TColors.Purple; Name: 'clPurple'),
    (Value: TColors.Teal; Name: 'clTeal'),
    (Value: TColors.Gray; Name: 'clGray'),
    (Value: TColors.Silver; Name: 'clSilver'),
    (Value: TColors.Red; Name: 'clRed'),
    (Value: TColors.Lime; Name: 'clLime'),
    (Value: TColors.Yellow; Name: 'clYellow'),
    (Value: TColors.Blue; Name: 'clBlue'),
    (Value: TColors.Fuchsia; Name: 'clFuchsia'),
    (Value: TColors.Aqua; Name: 'clAqua'),
    (Value: TColors.White; Name: 'clWhite'),

    (Value: TColors.MoneyGreen; Name: 'clMoneyGreen'),
    // Use LegacySkyBlue to maintain consistency in VCL colors
    (Value: TColors.LegacySkyBlue; Name: 'clSkyBlue'),
    (Value: TColors.Cream; Name: 'clCream'),
    (Value: TColors.MedGray; Name: 'clMedGray'),

    (Value: TColors.SysActiveBorder; Name: 'clActiveBorder'),
    (Value: TColors.SysActiveCaption; Name: 'clActiveCaption'),
    (Value: TColors.SysAppWorkSpace; Name: 'clAppWorkSpace'),
    (Value: TColors.SysBackground; Name: 'clBackground'),
    (Value: TColors.SysBtnFace; Name: 'clBtnFace'),
    (Value: TColors.SysBtnHighlight; Name: 'clBtnHighlight'),
    (Value: TColors.SysBtnShadow; Name: 'clBtnShadow'),
    (Value: TColors.SysBtnText; Name: 'clBtnText'),
    (Value: TColors.SysCaptionText; Name: 'clCaptionText'),
    (Value: TColors.SysDefault; Name: 'clDefault'),
    (Value: TColors.SysGradientActiveCaption; Name: 'clGradientActiveCaption'),
    (Value: TColors.SysGradientInactiveCaption; Name: 'clGradientInactiveCaption'),
    (Value: TColors.SysGrayText; Name: 'clGrayText'),
    (Value: TColors.SysHighlight; Name: 'clHighlight'),
    (Value: TColors.SysHighlightText; Name: 'clHighlightText'),
    (Value: TColors.SysHotLight; Name: 'clHotLight'),
    (Value: TColors.SysInactiveBorder; Name: 'clInactiveBorder'),
    (Value: TColors.SysInactiveCaption; Name: 'clInactiveCaption'),
    (Value: TColors.SysInactiveCaptionText; Name: 'clInactiveCaptionText'),
    (Value: TColors.SysInfoBk; Name: 'clInfoBk'),
    (Value: TColors.SysInfoText; Name: 'clInfoText'),
    (Value: TColors.SysMenu; Name: 'clMenu'),
    (Value: TColors.SysMenuBar; Name: 'clMenuBar'),
    (Value: TColors.SysMenuHighlight; Name: 'clMenuHighlight'),
    (Value: TColors.SysMenuText; Name: 'clMenuText'),
    (Value: TColors.SysNone; Name: 'clNone'),
    (Value: TColors.SysScrollBar; Name: 'clScrollBar'),
    (Value: TColors.Sys3DDkShadow; Name: 'cl3DDkShadow'),
    (Value: TColors.Sys3DLight; Name: 'cl3DLight'),
    (Value: TColors.SysWindow; Name: 'clWindow'),
    (Value: TColors.SysWindowFrame; Name: 'clWindowFrame'),
    (Value: TColors.SysWindowText; Name: 'clWindowText'));

function ColorToString(Color: TColor): string;
begin
  if not ColorToIdent(Color, Result) then
    FmtStr(Result, '%s%0.8x', [HexDisplayPrefix, Integer(Color)]);
end;

function StringToColor(const S: string): TColor;
var
  LColor: Integer;
begin
  if not IdentToColor(S, LColor) then
    Result := TColor(StrToInt(S))
  else
    Result := TColor(LColor);
end;

procedure GetColorValues(Proc: TGetStrProc);
var
  I: Integer;
begin
  for I := Low(Colors) to High(Colors) do Proc(Colors[I].Name);
end;

function ColorToIdent(Color: Integer; var Ident: string): Boolean;
begin
  Result := IntToIdent(Color, Ident, Colors);
end;

function IdentToColor(const Ident: string; var Color: Integer): Boolean;
begin
  Result := IdentToInt(Ident, Color, Colors);
end;

procedure RegisterColorIntegerConsts;
begin
  if not Assigned(FindIntToIdent(TypeInfo(TColor))) then
    RegisterIntegerConsts(TypeInfo(TColor), IdentToColor, ColorToIdent);
end;

{ Colors ======================================================================== }

const
  AlphaColors: array [0..147] of TIdentMapEntry = (
    (Value: Integer($FFF0F8FF); Name: 'claAliceblue'),
    (Value: Integer($FFFAEBD7); Name: 'claAntiquewhite'),
    (Value: Integer($FF00FFFF); Name: 'claAqua'),
    (Value: Integer($FF7FFFD4); Name: 'claAquamarine'),
    (Value: Integer($FFF0FFFF); Name: 'claAzure'),
    (Value: Integer($FFF5F5DC); Name: 'claBeige'),
    (Value: Integer($FFFFE4C4); Name: 'claBisque'),
    (Value: Integer($FF000000); Name: 'claBlack';),
    (Value: Integer($FFFFEBCD); Name: 'claBlanchedalmond'),
    (Value: Integer($FF0000FF); Name: 'claBlue'),
    (Value: Integer($FF8A2BE2); Name: 'claBlueviolet'),
    (Value: Integer($FFA52A2A); Name: 'claBrown'),
    (Value: Integer($FFDEB887); Name: 'claBurlywood'),
    (Value: Integer($FF5F9EA0); Name: 'claCadetblue'),
    (Value: Integer($FF7FFF00); Name: 'claChartreuse'),
    (Value: Integer($FFD2691E); Name: 'claChocolate'),
    (Value: Integer($FFFF7F50); Name: 'claCoral'),
    (Value: Integer($FF6495ED); Name: 'claCornflowerblue'),
    (Value: Integer($FFFFF8DC); Name: 'claCornsilk'),
    (Value: Integer($FFDC143C); Name: 'claCrimson'),
    (Value: Integer($FF00FFFF); Name: 'claCyan'),
    (Value: Integer($FF00008B); Name: 'claDarkblue'),
    (Value: Integer($FF008B8B); Name: 'claDarkcyan'),
    (Value: Integer($FFB8860B); Name: 'claDarkgoldenrod'),
    (Value: Integer($FFA9A9A9); Name: 'claDarkgray'),
    (Value: Integer($FF006400); Name: 'claDarkgreen'),
    (Value: Integer($FFA9A9A9); Name: 'claDarkgrey'),
    (Value: Integer($FFBDB76B); Name: 'claDarkkhaki'),
    (Value: Integer($FF8B008B); Name: 'claDarkmagenta'),
    (Value: Integer($FF556B2F); Name: 'claDarkolivegreen'),
    (Value: Integer($FFFF8C00); Name: 'claDarkorange'),
    (Value: Integer($FF9932CC); Name: 'claDarkorchid'),
    (Value: Integer($FF8B0000); Name: 'claDarkred'),
    (Value: Integer($FFE9967A); Name: 'claDarksalmon'),
    (Value: Integer($FF8FBC8F); Name: 'claDarkseagreen'),
    (Value: Integer($FF483D8B); Name: 'claDarkslateblue'),
    (Value: Integer($FF2F4F4F); Name: 'claDarkslategray'),
    (Value: Integer($FF2F4F4F); Name: 'claDarkslategrey'),
    (Value: Integer($FF00CED1); Name: 'claDarkturquoise'),
    (Value: Integer($FF9400D3); Name: 'claDarkviolet'),
    (Value: Integer($FFFF1493); Name: 'claDeeppink'),
    (Value: Integer($FF00BFFF); Name: 'claDeepskyblue'),
    (Value: Integer($FF696969); Name: 'claDimgray'),
    (Value: Integer($FF696969); Name: 'claDimgrey'),
    (Value: Integer($FF1E90FF); Name: 'claDodgerblue'),
    (Value: Integer($FFB22222); Name: 'claFirebrick'),
    (Value: Integer($FFFFFAF0); Name: 'claFloralwhite'),
    (Value: Integer($FF228B22); Name: 'claForestgreen'),
    (Value: Integer($FFFF00FF); Name: 'claFuchsia'),
    (Value: Integer($FFDCDCDC); Name: 'claGainsboro'),
    (Value: Integer($FFF8F8FF); Name: 'claGhostwhite'),
    (Value: Integer($FFFFD700); Name: 'claGold'),
    (Value: Integer($FFDAA520); Name: 'claGoldenrod'),
    (Value: Integer($FF808080); Name: 'claGray'),
    (Value: Integer($FF008000); Name: 'claGreen'),
    (Value: Integer($FFADFF2F); Name: 'claGreenyellow'),
    (Value: Integer($FF808080); Name: 'claGrey'),
    (Value: Integer($FFF0FFF0); Name: 'claHoneydew'),
    (Value: Integer($FFFF69B4); Name: 'claHotpink'),
    (Value: Integer($FFCD5C5C); Name: 'claIndianred'),
    (Value: Integer($FF4B0082); Name: 'claIndigo'),
    (Value: Integer($FFFFFFF0); Name: 'claIvory'),
    (Value: Integer($FFF0E68C); Name: 'claKhaki'),
    (Value: Integer($FFE6E6FA); Name: 'claLavender'),
    (Value: Integer($FFFFF0F5); Name: 'claLavenderblush'),
    (Value: Integer($FF7CFC00); Name: 'claLawngreen'),
    (Value: Integer($FFFFFACD); Name: 'claLemonchiffon'),
    (Value: Integer($FFADD8E6); Name: 'claLightblue'),
    (Value: Integer($FFF08080); Name: 'claLightcoral'),
    (Value: Integer($FFE0FFFF); Name: 'claLightcyan'),
    (Value: Integer($FFFAFAD2); Name: 'claLightgoldenrodyellow'),
    (Value: Integer($FFD3D3D3); Name: 'claLightgray'),
    (Value: Integer($FF90EE90); Name: 'claLightgreen'),
    (Value: Integer($FFD3D3D3); Name: 'claLightgrey'),
    (Value: Integer($FFFFB6C1); Name: 'claLightpink'),
    (Value: Integer($FFFFA07A); Name: 'claLightsalmon'),
    (Value: Integer($FF20B2AA); Name: 'claLightseagreen'),
    (Value: Integer($FF87CEFA); Name: 'claLightskyblue'),
    (Value: Integer($FF778899); Name: 'claLightslategray'),
    (Value: Integer($FF778899); Name: 'claLightslategrey'),
    (Value: Integer($FFB0C4DE); Name: 'claLightsteelblue'),
    (Value: Integer($FFFFFFE0); Name: 'claLightyellow'),
    (Value: Integer($FF00FF00); Name: 'claLime'),
    (Value: Integer($FF32CD32); Name: 'claLimegreen'),
    (Value: Integer($FFFAF0E6); Name: 'claLinen'),
    (Value: Integer($FFFF00FF); Name: 'claMagenta'),
    (Value: Integer($FF800000); Name: 'claMaroon'),
    (Value: Integer($FF66CDAA); Name: 'claMediumaquamarine'),
    (Value: Integer($FF0000CD); Name: 'claMediumblue'),
    (Value: Integer($FFBA55D3); Name: 'claMediumorchid'),
    (Value: Integer($FF9370DB); Name: 'claMediumpurple'),
    (Value: Integer($FF3CB371); Name: 'claMediumseagreen'),
    (Value: Integer($FF7B68EE); Name: 'claMediumslateblue'),
    (Value: Integer($FF00FA9A); Name: 'claMediumspringgreen'),
    (Value: Integer($FF48D1CC); Name: 'claMediumturquoise'),
    (Value: Integer($FFC71585); Name: 'claMediumvioletred'),
    (Value: Integer($FF191970); Name: 'claMidnightblue'),
    (Value: Integer($FFF5FFFA); Name: 'claMintcream'),
    (Value: Integer($FFFFE4E1); Name: 'claMistyrose'),
    (Value: Integer($FFFFE4B5); Name: 'claMoccasin'),
    (Value: Integer($FFFFDEAD); Name: 'claNavajowhite'),
    (Value: Integer($FF000080); Name: 'claNavy'),
    (Value: Integer($FFFDF5E6); Name: 'claOldlace'),
    (Value: Integer($FF808000); Name: 'claOlive'),
    (Value: Integer($FF6B8E23); Name: 'claOlivedrab'),
    (Value: Integer($FFFFA500); Name: 'claOrange'),
    (Value: Integer($FFFF4500); Name: 'claOrangered'),
    (Value: Integer($FFDA70D6); Name: 'claOrchid'),
    (Value: Integer($FFEEE8AA); Name: 'claPalegoldenrod'),
    (Value: Integer($FF98FB98); Name: 'claPalegreen'),
    (Value: Integer($FFAFEEEE); Name: 'claPaleturquoise'),
    (Value: Integer($FFDB7093); Name: 'claPalevioletred'),
    (Value: Integer($FFFFEFD5); Name: 'claPapayawhip'),
    (Value: Integer($FFFFDAB9); Name: 'claPeachpuff'),
    (Value: Integer($FFCD853F); Name: 'claPeru'),
    (Value: Integer($FFFFC0CB); Name: 'claPink'),
    (Value: Integer($FFDDA0DD); Name: 'claPlum'),
    (Value: Integer($FFB0E0E6); Name: 'claPowderblue'),
    (Value: Integer($FF800080); Name: 'claPurple'),
    (Value: Integer($FFFF0000); Name: 'claRed'),
    (Value: Integer($FFBC8F8F); Name: 'claRosybrown'),
    (Value: Integer($FF4169E1); Name: 'claRoyalblue'),
    (Value: Integer($FF8B4513); Name: 'claSaddlebrown'),
    (Value: Integer($FFFA8072); Name: 'claSalmon'),
    (Value: Integer($FFF4A460); Name: 'claSandybrown'),
    (Value: Integer($FF2E8B57); Name: 'claSeagreen'),
    (Value: Integer($FFFFF5EE); Name: 'claSeashell'),
    (Value: Integer($FFA0522D); Name: 'claSienna'),
    (Value: Integer($FFC0C0C0); Name: 'claSilver'),
    (Value: Integer($FF87CEEB); Name: 'claSkyblue'),
    (Value: Integer($FF6A5ACD); Name: 'claSlateblue'),
    (Value: Integer($FF708090); Name: 'claSlategray'),
    (Value: Integer($FF708090); Name: 'claSlategrey'),
    (Value: Integer($FFFFFAFA); Name: 'claSnow'),
    (Value: Integer($FF00FF7F); Name: 'claSpringgreen'),
    (Value: Integer($FF4682B4); Name: 'claSteelblue'),
    (Value: Integer($FFD2B48C); Name: 'claTan'),
    (Value: Integer($FF008080); Name: 'claTeal'),
    (Value: Integer($FFD8BFD8); Name: 'claThistle'),
    (Value: Integer($FFFF6347); Name: 'claTomato'),
    (Value: Integer($FF40E0D0); Name: 'claTurquoise'),
    (Value: Integer($FFEE82EE); Name: 'claViolet'),
    (Value: Integer($FFF5DEB3); Name: 'claWheat'),
    (Value: Integer($FFFFFFFF); Name: 'claWhite'),
    (Value: Integer($FFF5F5F5); Name: 'claWhitesmoke'),
    (Value: Integer($FFFFFF00); Name: 'claYellow'),
    (Value: Integer($FF9ACD32); Name: 'claYellowgreen'),
    (Value: Integer($0); Name: 'claNull')
  );

{ TAlphaColor string functions }

procedure GetAlphaColorValues(Proc: TGetStrProc);
var
  I: Integer;
begin
  for I := Low(AlphaColors) to High(AlphaColors) do
    Proc(AlphaColorToString(TAlphaColor(AlphaColors[I].Value)));
end;

function AlphaColorToString(Value: TAlphaColor): string;
begin
  AlphaColorToIdent(Integer(Value), Result);
  if Result.Chars[0] = 'x' then
    Result := '#' + Result.SubString(1)
  else
    Result := Result.Remove(0, 3);
end;

function StringToAlphaColor(const Value: string): TAlphaColor;
var
  LValue: string;
  LColor: Integer;
begin
  LValue := Value;
  if LValue = #0 then
    LValue := '$0'
  else if (LValue <> '') and ((LValue.Chars[0] = '#') or (LValue.Chars[0] = 'x')) then
    LValue := '$' + LValue.SubString(1);

  if (not IdentToAlphaColor('cla' + LValue, LColor)) and (not IdentToAlphaColor(LValue, LColor)) then
    Result := TAlphaColor(StrToInt64(LValue))
  else
    Result := TAlphaColor(LColor);
end;

function AlphaColorToIdent(Color: Integer; var Ident: string): Boolean;
begin
  Result := IntToIdent(Color, Ident, AlphaColors);
  if not Result then
  begin
    Ident := 'x' + IntToHex(Color, 8);
    Result := True;
  end;
end;

function IdentToAlphaColor(const Ident: string; var Color: Integer): Boolean;
var
  LIdent: string;
begin
  LIdent := Ident;
  if (LIdent.Length > 0) and (LIdent.Chars[0] = 'x') then
  begin
    Color := Integer(StringToAlphaColor(LIdent));
    Result := True;
  end else
    Result := IdentToInt(LIdent, Color, AlphaColors);
  // Allow "clXXXX" constants and convert it to TAlphaColor
  if not Result and (LIdent.Length > 3) then
  begin
    LIdent := LIdent.Insert(2, 'a');
    Result := IdentToInt(LIdent, Integer(Color), AlphaColors);
  end;
end;

procedure RegisterAlphaColorIntegerConsts;
begin
  if not Assigned(FindIntToIdent(TypeInfo(TAlphaColor))) then
    RegisterIntegerConsts(TypeInfo(TAlphaColor), IdentToAlphaColor, AlphaColorToIdent);
end;

{ Colors }

function AlphaColorToColor(const Color: TAlphaColor): TColor;
begin
  TColorRec(Result).R := TAlphaColorRec(Color).R;
  TColorRec(Result).G := TAlphaColorRec(Color).G;
  TColorRec(Result).B := TAlphaColorRec(Color).B;
  TColorRec(Result).A := 0;
end;

function AppendColor(Start, Stop: TAlphaColor): TAlphaColor;
begin
  if TAlphaColorRec(Start).A + TAlphaColorRec(Stop).A < $FF then
    TAlphaColorRec(Result).A := TAlphaColorRec(Start).A + TAlphaColorRec(Stop).A
  else
    TAlphaColorRec(Result).A := $FF;
  if TAlphaColorRec(Start).R + TAlphaColorRec(Stop).R < $FF then
    TAlphaColorRec(Result).R := TAlphaColorRec(Start).R + TAlphaColorRec(Stop).R
  else
    TAlphaColorRec(Result).R := $FF;
  if TAlphaColorRec(Start).G + TAlphaColorRec(Stop).G < $FF then
    TAlphaColorRec(Result).G := TAlphaColorRec(Start).G + TAlphaColorRec(Stop).G
  else
    TAlphaColorRec(Result).G := $FF;
  if TAlphaColorRec(Start).B + TAlphaColorRec(Stop).B < $FF then
    TAlphaColorRec(Result).B := TAlphaColorRec(Start).B + TAlphaColorRec(Stop).B
  else
    TAlphaColorRec(Result).B := $FF;
end;

function SubtractColor(Start, Stop: TAlphaColor): TAlphaColor;
begin
  if TAlphaColorRec(Start).A - TAlphaColorRec(Stop).A < $FF then
    TAlphaColorRec(Result).A := TAlphaColorRec(Start).A - TAlphaColorRec(Stop).A
  else
    TAlphaColorRec(Result).A := $FF;
  if TAlphaColorRec(Start).R - TAlphaColorRec(Stop).R < $FF then
    TAlphaColorRec(Result).R := TAlphaColorRec(Start).R - TAlphaColorRec(Stop).R
  else
    TAlphaColorRec(Result).R := $FF;
  if TAlphaColorRec(Start).G - TAlphaColorRec(Stop).G < $FF then
    TAlphaColorRec(Result).G := TAlphaColorRec(Start).G - TAlphaColorRec(Stop).G
  else
    TAlphaColorRec(Result).G := $FF;
  if TAlphaColorRec(Start).B - TAlphaColorRec(Stop).B < $FF then
    TAlphaColorRec(Result).B := TAlphaColorRec(Start).B - TAlphaColorRec(Stop).B
  else
    TAlphaColorRec(Result).B := $FF;
end;

function RGBtoBGR(const C: TAlphaColor): TAlphaColor;
begin
  Result := C;
  TAlphaColorRec(Result).R := TAlphaColorRec(C).B;
  TAlphaColorRec(Result).B := TAlphaColorRec(C).R;
end;

function CorrectColor(const C: TAlphaColor): TAlphaColor;
begin
  Result := C;
{$IFNDEF MSWINDOWS}
  TColorRec(Result).R := TColorRec(C).B;
  TColorRec(Result).B := TColorRec(C).R;
{$ENDIF}
end;

function PremultiplyAlpha(const C: TAlphaColor): TAlphaColor;
begin
  if TAlphaColorRec(C).A = 0 then
    Result := 0
  else if TAlphaColorRec(C).A = $FF then
    Result := C
  else
  begin
    TAlphaColorRec(Result).R := trunc(TAlphaColorRec(C).R * (TAlphaColorRec(C).A / $FF));
    TAlphaColorRec(Result).G := trunc(TAlphaColorRec(C).G * (TAlphaColorRec(C).A / $FF));
    TAlphaColorRec(Result).B := trunc(TAlphaColorRec(C).B * (TAlphaColorRec(C).A / $FF));
    TAlphaColorRec(Result).A := TAlphaColorRec(C).A;
  end;
end;

function UnpremultiplyAlpha(const C: TAlphaColor): TAlphaColor;
begin
  if TAlphaColorRec(C).A = 0 then
    Result := 0
  else if TAlphaColorRec(C).A = $FF then
    Result := C
  else
  begin
    TAlphaColorRec(Result).R := trunc(TAlphaColorRec(C).R / (TAlphaColorRec(C).A / $FF));
    TAlphaColorRec(Result).G := trunc(TAlphaColorRec(C).G / (TAlphaColorRec(C).A / $FF));
    TAlphaColorRec(Result).B := trunc(TAlphaColorRec(C).B / (TAlphaColorRec(C).A / $FF));
    TAlphaColorRec(Result).A := TAlphaColorRec(C).A;
  end;
end;

function MakeColor(const C: TAlphaColor; const AOpacity: Single): TAlphaColor;
begin
  Result := C;
  if AOpacity < 1 then
    TAlphaColorRec(Result).A := trunc(TAlphaColorRec(C).A * AOpacity);
end;

function MakeColor(R, G, B: Byte; A: Byte = $FF): TAlphaColor;
begin
  TAlphaColorRec(Result).R := R;
  TAlphaColorRec(Result).G := G;
  TAlphaColorRec(Result).B := B;
  TAlphaColorRec(Result).A := A;
end;

function ChangeHSL(const C: TAlphaColor; dH, dS, dL: Single): TAlphaColor;
var
  H, S, L: Single;
begin
  RGBtoHSL(C, H, S, L);
  H := H + dH;
  if H < 0 then
    H := H + 1
  else if H > 1 then
    H := H - 1;
  S := S + dS;
  if S < 0 then
    S := 0
  else if S > 1 then
    S := 1;
  L := L + dL;
  if L < 0 then
    L := 0
  else if L > 1 then
    L := 1;
  Result := HSLtoRGB(H, S, L);
  TAlphaColorRec(Result).A := TAlphaColorRec(C).A;
end;

function HSLtoRGB(H, S, L: Single): TAlphaColor;
  function Clip(Value: Single): Single;
  begin
    if Value < 0.0 then
      Result := 0.0
    else
    if Value > 1.0 then
      Result := 1.0
    else
      Result := Value;
  end;

  function hue2rgb(P, q, t: Single): Single;
  begin
    if (t < 0) then
      t := t + 1;
    if (t > 1) then
      t := t - 1;
    if (t < 1 / 6) then
    begin
      Result := P + (q - P) * 6 * t;
      Exit;
    end;
    if (t < 1 / 2) then
    begin
      Result := q;
      Exit;
    end;
    if (t < 2 / 3) then
    begin
      Result := P + (q - P) * (2 / 3 - t) * 6;
      Exit;
    end;
    Result := Clip(P);
  end;

var
  R, G, B: Single;
  q, P: Single;
begin
  if (S = 0) then
  begin
    L := Clip(L);
    R := L;
    G := L;
    B := L;
  end else
  begin
    if (L < 0.5) then
      q := L * (1 + S)
    else
      q := L + S - L * S;
    P := 2 * L - q;
    R := hue2rgb(P, q, H + 1 / 3);
    G := hue2rgb(P, q, H);
    B := hue2rgb(P, q, H - 1 / 3);
  end;
  Result := MakeColor(round(R * $FF), round(G * $FF), round(B * $FF));
end;

procedure RGBtoHSL(RGB: TAlphaColor; out H, S, L: Single);
var
  R, G, B: Single;
  D, mx, mn: Single;

  function _Max(AVarFirst, AVarSecond : Single) : Single ;
  begin
    if AVarFirst < AVarSecond then
      Result := AVarSecond
    else
      Result := AVarFirst ;
  end ;

  function _Min(AVarFirst, AVarSecond : Single) : Single ;
  begin
    if AVarFirst > AVarSecond then
      Result := AVarSecond
    else
      Result := AVarFirst ;
  end ;

begin
  R := TAlphaColorRec(RGB).R / $FF;
  G := TAlphaColorRec(RGB).G / $FF;
  B := TAlphaColorRec(RGB).B / $FF;
  mx := _Max(_Max(R, G), B);
  mn := _Min(_Min(R, G), B);
  H := (mx + mn) / 2;
  L := H;
  S := H;
  if (mx = mn) then
  begin
    S := 0;
    H := 0;
  end
  else
  begin
    D := mx - mn;
    if L > 0.5 then
      S := D / (2 - mx - mn)
    else
      S := D / (mx + mn);
    if (mx = R) then
      H := (G - B) / D
    else if (mx = G) then
      H := (B - R) / D + 2
    else
      H := (R - G) / D + 4;
    H := H / 6;
    if H < 0 then
      H := H + 1;
  end;
end;

end.
