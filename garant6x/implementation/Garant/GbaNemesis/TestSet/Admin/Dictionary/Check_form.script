USES
 WordsTranslation.script
 @\SysUtils.script
 @\CommonSystem.script
 @\Controls.script
 @\Forms.script
 Forms.script
 HLTCLike.script
;

PROCEDURE "Проверить расширенно параметры и контекстное меню контролов, содержимое тулбаров для формы" BOOLEAN IN aToolbar STRING IN aForm

OBJECT FUNCTION "Форма от которой начинаем перебор"
 "Найти окно по имени {(aForm)}" =: Result  
;

 PROCEDURE Действия_с_контролом OBJECT IN aCtrl
  Если ( aCtrl "Объект существует?" ) то
  ( Если ( ( aCtrl  ЯВЛЯЕТСЯ class::TvcmDockPanel )
   ИЛИ ( aCtrl ЯВЛЯЕТСЯ class::TvcmToolButtonDef )
   ИЛИ ( aCtrl ЯВЛЯЕТСЯ class::TvcmDockDef )
   ИЛИ ( aCtrl ЯВЛЯЕТСЯ class::TvcmToolbar ) 
   ИЛИ ( aCtrl "Узнать имя контрола" РАВНО '' ) ) то
    выходим
   иначе
    ( aCtrl .
     Если ( aCtrl "Перейти к родительской форме" "Объект существует?" ) то
      ( 'Parent: ' aCtrl "Родительский объект" ToPrintable Cat . )
     aCtrl "Заголовок контрола" .
     Если ( ( aCtrl ЯВЛЯЕТСЯ class::TPanel ) 
     ИЛИ ( aCtrl ЯВЛЯЕТСЯ class::TvtPanel ) ) то
      ( "Аккуратно сравнить с эталоном целочисленные свойства {( [ 'BevelWidth' 'BevelOuter' 'BevelInner' ] )} объекта {(aCtrl)}" )
     Если ( ( aCtrl "Перейти к родительской форме" "Объект не существует?" )
     И ( aCtrl "Форма от которой начинаем перебор" ?== ) ) то   
      ( 'Position:' 'Position' aCtrl  "Узнать численную переменную объекта" IntToStr Cat .
       "Вывести в эталон размеры контрола {(aCtrl)} с округлением до {(5)}" 
      )     
      иначе
       ( "Вывести в эталон размеры и координаты контрола {(aCtrl)} с округлением до {(5)}" ) 
       // исключаем координаты
     'Visible: ' aCtrl "Узнать, виден ли контрол" ToPrintable Cat .
     "Сравнить с эталоном MinHeight, MaxHeight, MinWidth и MaxWidth для свойства constraints контрола {(aCtrl)}, если они отличны от нуля"
     "Аккуратно сравнить с эталоном целочисленное свойство {('BorderStyle')} объекта {(aCtrl)}"
     "Аккуратно сравнить с эталоном целочисленные свойства {( [ 'TabOrder' 'ImageIndex' 'TabStop' 'Align' 'Anchors' 'BorderWidth' 'ReadOnly' ] )} объекта {(aCtrl)}"
     "Аккуратно сравнить с эталоном объектное свойство {('Images')} объекта {(aCtrl)}"
    Если ( aCtrl ЯВЛЯЕТСЯ class::TvtLabel ) то
     ( "Аккуратно сравнить с эталоном целочисленное свойство {('Transparent')} объекта {(aCtrl)}"
      "Аккуратно сравнить с эталоном логическое свойство {( 'AutoSize' )} объекта {(aCtrl)}" 
     ) // Transparent
    Если ( aCtrl ЯВЛЯЕТСЯ class::TnscTreeView ) то
     ( 'Вертикальный скроллер:' aCtrl "Узнать, есть ли у контрола вертикальный скроллер" ToPrintable Cat .
      'Горизонтальный скроллер:' aCtrl "Узнать, есть ли у контрола горизонтальный скроллер" ToPrintable Cat .
     ) // скроллеры для деревьев и редакторов
     "Отделить текст в эталоне"
     'Настройки шрифта: ' . 
     "Сравнить настройки шрифтов с эталоном для контрола {( aCtrl  )}"
     Если ( aCtrl ЯВЛЯЕТСЯ class::Ttb97MoreButton ) то
      ( 'Не сравниваем контекстное меню' . )
     иначе
      ( "Сравнить контекстное меню для контрола {(aCtrl)} для точки {( 0 0 )} с эталоном" )
    ) // исключаем TvcmDockPanel
  '---------------' .
  ) // проверка контрола
 ; // Действия_с_контролом

 '--------------- Контрол в фокусе: ------------------' .
 "Контрол в фокусе" .
 'Его полная иерархия:' .
 "Проверить иерархию вложенности текущего контрола"
 '--------------- Перечень дочерних контролов, их размеры, координаты и контекстное меню: -------------------' .
 'Форма:' .
 "Аккуратно сравнить с эталоном целочисленное свойство {('BorderStyle')} объекта {("Форма от которой начинаем перебор")}"
 "Аккуратно сравнить с эталоном значение объектного свойства {('ActiveControl')} объекта {("Форма от которой начинаем перебор")}"
 '---------------' .
 "выполнить для всех контролов {("Форма от которой начинаем перебор")}" Действия_с_контролом 

 aToolbar ? (  '--------------- Содержимое статусбара и тулбаров: -------------------' .
 'Эталон статусбара: ' . 
 "Сравнить статусбар с эталоном"
 "Отделить текст в эталоне"
 "Сравнить содержимое тулбаров с эталоном полностью" )
; // "Проверить расширенно параметры и контекстное меню контролов, содержимое тулбаров для окна

: "Проверить параметры и контекстное меню контролов, содержимое тулбаров для формы" STRING IN aForm
 "Очистить буфер обмена"
 "Проверить расширенно параметры и контекстное меню контролов, содержимое тулбаров {(true)} для формы {(aForm)}"
; // "Проверить параметры и контекстное меню контролов, содержимое тулбаров для формы"

: "Проверить параметры и контекстное меню контролов для формы" STRING IN aForm
 "Очистить буфер обмена"
 "Проверить расширенно параметры и контекстное меню контролов, содержимое тулбаров {(false)} для формы {(aForm)}"
; // "Проверить параметры и контекстное меню контролов для формы"

PROCEDURE "Сравнить содержимое тулбара  на форме с эталоном" STRING IN aForm

 PROCEDURE Действия OBJECT IN aObj
  ( aObj ЯВЛЯЕТСЯ class::TvcmToolbar ) ? (
   ( aObj pop:Control:Visible И ( aObj pop:Control:ControlCount БОЛЬШЕ 1 ) ) ? (
    aObj .
    'Order = ' 'DockRow' aObj pop:object:GetOrdProp IntToStr Cat .
    '---------------' .
    aObj EnumerateControls ( OBJECT IN aControl
    ( aControl "НЕ ЯВЛЯЕТСЯ" class::Ttb97MoreButton ) ? (
      aControl .
      ( aControl "НЕ ЯВЛЯЕТСЯ" class::TvcmComboBox ) ? (
       aControl pop:Control:Caption .
       'Активность:' aControl "Убедиться, что контрол активен" ToPrintable Cat .
       '---' . 
      )
     )
    )
    '' .
   )
  )
 ;

 OBJECT VAR work_form
 "Найти окно по имени {(aForm)}" >>> work_form
 work_form "выполнить для всех контролов" Действия
; // "Сравнить содержимое тулбара  на форме с эталоном"
