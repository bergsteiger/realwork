USES
 axiom:Wnd
 axiom:Component
 axiom:Tree
 axiom:Node
 axiom:форма
 axiom:контрол
 axiom:TefUserList
 
 WordsTranslation.script
 Forms.script
 @\SysUtils.script
 @\CommonSystem.script
 @\Controls.script
 Controls.script
 @\Mouse.script
 @\ExCtrls.script
;

: "Установить окно модуля Администратора на передний план"
  VAR l_Handle
  'ГАРАНТ аэро - Администрирование пользователей ' FindWindowByCaption =: l_Handle
  l_Handle 0 ?!= ?ASSURE 'Не нашли окно Админа!'
  l_Handle wnd:SetForeground
;

PROCEDURE "Установить фокус в модуль Администратора"
 OBJECT VAR l_Form
 ANYUSERTYPE форма::UserList vcm:FindForm ?ASSURE 'Не найдена форма UserList'
 >>> l_Form
 // l_Form pop:control:SetFocus
 //  DROP
 l_Form .TefUserList.trUserList pop:Control:SetFocus
 ?ASSURE 'Не удалось установить фокус в первоначальное состояние!'
; // "Установить фокус в модуль Администратора"

PROCEDURE "Приготовиться к выполнению следующего теста"
 "Установить фокус в модуль Администратора"
;

PROCEDURE "Нажать кнопку Сохранить"
 OBJECT VAR "Кнопка Сохранить"
 "Найти контрол {(контрол::bt_enResult_opSave)} на форме {("Главное окно")}" =: "Кнопка Сохранить"
 "Кнопка Сохранить" "Объект существует?" ?ASSURE 'Не нашли кнопку Сохранить'
 "Кнопка Сохранить" "Кликнуть"
 OnTest
;  

PROCEDURE "Нажать кнопку Отмена"
 OBJECT VAR "Кнопка Отмена"
 "Найти контрол {(контрол::bt_enResult_opCancel)} на форме {("Главное окно")}" =: "Кнопка Отмена"
 "Кнопка Отмена" "Объект существует?" ?ASSURE 'Не нашли кнопку Отмена'
 "Кнопка Отмена" "Кликнуть"
 OnTest
; // "Нажать кнопку Отмена"

: "Нажать Del"
 "Нажать {('Del')}"
;

BOOLEAN FUNCTION "Найти ноду в дереве" STRING IN aContext STRING IN aControl
 false =: Result
 
 STRING VAR l_Name
 
 focused:control:push pop:Component:Name =: l_Name
 ( l_Name РАВНО aControl ) ?ASSURE
  [ 'Почему то в фокусе ' l_Name ' а не ' aControl ]

 : "Ищем ноду по контексту" IN aProc
  INTEGER VAR "Предельное количество"
  500 >>> "Предельное количество"
 
  BOOLEAN VAR Сделано
  НЕТ >>> Сделано
 
  INTERFACE VAR l_Node
  
  VAR l_CompareOp
   @ StartsStr =: l_CompareOp
 
  TRY  
   ПОКА ( НЕ Сделано И ( "Предельное количество" >0 ) )
   BEGIN
    --! "Предельное количество"
    aControl byname:control:push tree:CurrentNode >>> l_Node
    
    STRING VAR l_Text
    l_Node Node:Text =: l_Text
        
    Если ( aContext l_Text l_CompareOp DO ) то 
     ( ДА >>> Сделано
     true =: Result )
    иначе
     (
      TRY
       aProc DO
      EXCEPT
       // - наверное добежали до конца дерева
       ДА >>> Сделано
      END 
     )
   END
  
  FINALLY 
   nil =: l_Node
  END
 ; // "Ищем ноду по контексту"
 
 @ "Стрелка вниз" "Ищем ноду по контексту"
; 

: "Установить фокус во вкладку Группы пользователей"
 "Перевести фокус в {(контрол::GroupsTree)} на форме {(форма::GroupList)}"
; // "Установить фокус во вкладку Группы пользователей"

PROCEDURE "Переключиться на группу с именем" STRING IN aNameFindGroup
 "Установить фокус во вкладку Группы пользователей"
 "Переместиться в начало дерева"
 "Найти ноду {(aNameFindGroup)} в дереве {('GroupsTree')}" ?ASSURE 'Не нашли группу!'
;

PROCEDURE "Удалить ноду в дереве" STRING IN aContext STRING IN aControl
 "Дождаться переключения вкладок"
 "Переместиться в начало дерева"
 "Найти ноду {(aContext)} в дереве {(aControl)}" ?ASSURE [ 'Не нашли ссылку ' aContext ' для удаления!' ]
 "Ответить один раз Да для {(@ "Нажать Del")}" 
 OnTest
;

STRING FUNCTION "Имя теста"
 Result := ( "Полный путь скрипта" "Имя скрипта" )
; 

VOID WORDWORKER "Создать пользователя и выполнить с ним" STRING IN aNameNewUser
 "Дождаться переключения вкладок"
 "Установить фокус в модуль Администратора"
 "Создать нового пользователя"
 "Найти контрол {('edLogin')} на форме {("Главное окно")}" "Установить фокус"
 "Ввести {(aNameNewUser)}"  
 "Найти контрол {('edUserName')} на форме {("Главное окно")}" "Установить фокус"
 "Ввести {(aNameNewUser)}"  
 "Нажать кнопку Сохранить"
 "Дождаться переключения вкладок"
 TRY
  ( WordToWork DO ) 
 FINALLY
  "Дождаться переключения вкладок"
  "Установить фокус в модуль Администратора"
  "Удалить ноду {(aNameNewUser)} в дереве {('trUserList')}"
 END
; 

: "При помощи Shift выделяем элементов" IN aNum
 VAR l_Finish
 "Контрол в фокусе" "Узнать номер позиции фокуса в списке" aNum + >>> l_Finish // Подсчитываем конец выделения
 l_Finish "Контрол в фокусе" "Выделить с помощью Shift"  // Ну и выделяем
; // "При помощи Shift выделяем элементов"

VOID WORDWORKER "Создать группу и выполнить с ней" STRING IN aNameNewGroup
 "Установить фокус во вкладку Группы пользователей"
 @ ( "Создать новую группу" ) MODAL ( 
 "Ввести {(aNameNewGroup)}"  
 "Нажать {('Enter')}" )
 TRY
  ( WordToWork DO ) 
 FINALLY
  "Дождаться переключения вкладок"
  "Установить фокус во вкладку Группы пользователей"
  "Удалить ноду {(aNameNewGroup)} в дереве {('GroupsTree')}"
 END
; 

PROCEDURE "Проверить доступность кнопки" STRING IN aControl
 OBJECT VAR l_Control
 "Найти контрол {(aControl)} по имени на главной форме" =: l_Control
 l_Control pop:Control:Enabled .
;

PROCEDURE "Узнать, существует ли контрол и проверить его доступность" IN aControl
 "Найти контрол по имени {(aControl)} на главной форме" 
 [ 'Свойство Enable контрола ' aControl  ' имеет значение: ' ] strings:Cat .
 "Проверить доступность кнопки {(aControl)}"
;

VOID WORDWORKER "Открыть диалог 'Разрешить/Запретить авторегистрацию новых пользователей' и выполнить"
@ "Разрешить/Запретить авторегистрацию новых пользователей" MODAL ( WordToWork DO )
;

VOID WORDWORKER "Открыть диалог 'Новая группа' и выполнить"
 @ ( "Создать новую группу" ) MODAL ( WordToWork DO )
;

PROCEDURE "Переименовать текущую группу в" STRING IN aNewNameGroup
 "Установить фокус во вкладку Группы пользователей"
 @ ( "Переименовать группу" ) MODAL ( 
  "Ввести {(aNewNameGroup)}"
  "Нажать {('Enter')}"
 )
 9 раз ( "Дождаться переключения вкладок" )
 // чтобы успел появиться диалог с подтверждением (диалог будет, если есть пользователь в группе)
; 

PROCEDURE "Переименовать текущую группу в 'Users'"
 "Переименовать текущую группу в {('Users')}"
 "Дождаться переключения вкладок"
 OnTest
;

PROCEDURE "Сравнить с эталоном группу у пользователя " STRING IN aNameUser
 "Дождаться переключения вкладок"
 "Установить фокус в модуль Администратора"
 "Найти ноду {(aNameUser)} в дереве {('trUserList')}" ? (
  'Пользователь: ' "Контрол в фокусе" "Заголовок контрола" ToPrintable Cat .
  "Найти контрол {('edGroup')} на форме {("Главное окно")}" "Установить фокус"
  'Группа: ' "Контрол в фокусе" "Заголовок контрола" ToPrintable Cat .
 )
;

PROCEDURE "Переименовать текущую группу в 'Users' (подтвердить изменение настроек)"
 "Ответить один раз Да для {( @ "Переименовать текущую группу в 'Users'" )}"
; 

VOID WORDWORKER "Удалить новые группы после выполнения"
 TRY
  ( WordToWork DO )
 FINALLY
  "Дождаться переключения вкладок"
  "Установить фокус во вкладку Группы пользователей"
  "Переместиться в начало дерева"
  "Контрол в фокусе" tree:GetItemsCount раз (
   "Дождаться переключения вкладок"
   Если ( "Контрол в фокусе" tree:CurrentNode Node:Text РАВНО 'Все' ! ) то ( 
    "Ответить один раз Да для {(@ "Нажать Del")}"  "Дождаться переключения вкладок"
    OnTest )
   иначе ( "Стрелка вниз" )
  ) 
 END
;

PROCEDURE "Создать группу с именем " STRING IN aNameNewGroup
 "Установить фокус во вкладку Группы пользователей"
 "Открыть диалог 'Новая группа' и выполнить" (
  "Ввести {(aNameNewGroup)}"  
  "Нажать {('Enter')}"
 )
; 
