USES
 WordsTranslation.script
 @\SysUtils.script
 @\Controls.script
 Forms.script
;

BOOLEAN FUNCTION "Удалось перевести фокус в ?" STRING IN aControl

 false =: Result

 : Действия OBJECT IN aObj
  Если ( aControl РАВНО ( aObj pop:Control:Name ) ) то
   ( aObj pop:Control:SetFocus =: Result
    "Закончить перебор контролов"
   )
 ;

 "Главное окно" "Выполнить для всех контролов" Действия
;

: "Привести координаты для контрола к экранным" OBJECT IN X OBJECT IN Y OBJECT IN aControl
 X DO Y DO aControl pop:Control:ClientToScreen =:^ Y =:^ X
;

PROCEDURE "Убедиться, что текущий контрол: " STRING IN aCtrl
 "Контрол в фокусе" "Имя компонента" РАВНО aCtrl ?ASSURE
 [ 'Фокус почему-то не в ' aCtrl ', а в ' "Контрол в фокусе" "Имя компонента" ':' "Контрол в фокусе" pop:object:ClassName ]
;

VOID WORDWORKER "Убедиться, что контрол не сместился по оси Х после выполнения" OBJECT IN aCtrl
  INTEGER VAR aFirstCoordX
  aCtrl pop:Control:Left =: aFirstCoordX 
 ( WordToWork DO )
  "Дождаться переключения вкладок"
 aCtrl pop:Control:Left РАВНО aFirstCoordX ?ASSURE [ aCtrl "Имя компонента" ' сместился по координате X!' ]
;

PROCEDURE "Сравнить с эталоном, зажата ли кнопка на форме " STRING IN aButton OBJECT IN aForm
 OBJECT VAR "Нужная кнопка"
 "Найти контрол {(aButton)} на форме {(aForm)}" =: "Нужная кнопка" 
 [ 'Кнопка ' aButton ' зажата?' ] strings:Cat . 
 "Узнать численную {('Down')} переменную объекта {("Нужная кнопка")}" 0 ?!= .
;

PROCEDURE "Сравнить с эталоном активность контрола " OBJECT IN Ctrl
 [ 'Активность контрола: ' Ctrl pop:Control:Enabled ToPrintable ] strings:Cat .
;

ARRAY FUNCTION "Экранные координаты контрола" STRING IN aCtrl
 INTEGER VAR X
 INTEGER VAR Y
 OBJECT VAR CurrentControl
 "Найти контрол {(aCtrl)} на форме {("Текущая активная вкладка")}" =: CurrentControl
 "Померить ширину {(CurrentControl)}" >>> X
 "Померить высоту {(CurrentControl)}" >>> Y
 "Привести координаты {( @ X @ Y )} для контрола {(CurrentControl)} к экранным" 
 [ 
  X
  Y
 ]
 >>> Result 
; 

PROCEDURE "Сравнить с эталоном экранные координаты контрола " STRING IN aCtrl
 ARRAY VAR aMass
 "Экранные координаты контрола {(aCtrl)}" =: aMass
 'Координата X (правая сторона): ' 0 aMass [i] ToPrintable Cat .
 'Координата Y (нижняя сторона): ' 1 aMass [i] ToPrintable Cat .
;