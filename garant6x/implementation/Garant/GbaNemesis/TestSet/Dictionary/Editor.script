USES
 axiom:Tests
 axiom:EVD
 
 WordsTranslation.script
 @\SysUtils.script
 @\EditorCommon.script
 F1ControlsDefinition.script
;

: "Перейти на параграф вверх"
 "Убедиться, что фокус в редакторе"
 "Перейти на параграф вверх в редакторе {(focused:control:push)}"
; 

: "Перейти на параграф вниз"
 "Убедиться, что фокус в редакторе"
 "Перейти на параграф вниз в редакторе {(focused:control:push)}"
; 

INTEGER VAR g_EtalonCount
//[EXECUTE] 
( 0 >>> g_EtalonCount )

: "Сравнить текст редактора с эталоном" OBJECT IN anEditor
 ++! g_EtalonCount
 OnTest
 VAR l_File
 Если ( g_EtalonCount РАВНО 1 ) то
  ( script:FileName '.evd' sysutils:ChangeFileExt )
 иначе
  ( script:FileName '.' g_EtalonCount IntToStr Cat '.evd' Cat sysutils:ChangeFileExt )
 sysutils:ExtractFileName >>> l_File
 l_File anEditor pop:editor:TextToFile
 l_File '%' tests:CheckEtalon
;

: "Сравнить текст с эталоном"
 "Сравнить текст редактора {(DocumentText:push)} с эталоном"
;

: "Сравнить абзац редактора с эталоном" OBJECT IN anEditor
 anEditor pop:editor:CurrentText #10 '[разрыв строки]' string:Replace .
;

: "Сравнить абзац текущего редактора с эталоном"
 "Убедиться, что фокус в редакторе"
 "Сравнить абзац редактора {(focused:control:push)} с эталоном"
; 

: "Сравнить выделенный текст текущего редактора с эталоном"
 "Убедиться, что фокус в редакторе"
 "Сравнить выделенный текст редактора {(focused:control:push)} с эталоном"
; // "Сравнить выделенный текст текущего редактора с эталоном"

: "Сравнить выделенный текст редактора с эталоном в формате" OBJECT IN anEditor STRING INTEGER IN aFormat
 cc:Copy
 // - затычка, иначе не будет правильно имя формата в код формата преобразовываться
 aFormat anEditor pop:editor:GetSelectionTextInFormat .
;

: "Сравнить выделенный текст редактора с эталоном в формате HTML с заменой непечатаемых символов"
 "Убедиться, что фокус в редакторе"
 CF_HTML "Контрол в фокусе" pop:editor:GetSelectionTextInFormat "Заменить непечатаемые символы" .
; // "Сравнить выделенный текст редактора с эталоном в формате HTML с заменой непечатаемых символов"

: "Сравнить выделенный текст текущего редактора с эталоном в формате" STRING INTEGER IN aFormat
 "Убедиться, что фокус в редакторе"
 "Сравнить выделенный текст редактора {(focused:control:push)} с эталоном в формате {(aFormat)}"
;

: "Проверяем отсутствие комментария в редакторе" OBJECT IN anEditor
 anEditor pop:editor:HasComment ! ?ASSURE 'Комментарий удалить не удалось'
;

: "Проверяем отсутствие комментария в текущем редакторе"
 "Убедиться, что фокус в редакторе"
 "Проверяем отсутствие комментария в редакторе {(focused:control:push)}" 
;

: "Удалить текущий комментарий и проверить что он удалился"
 "Убедиться, что фокус в редакторе"
 focused:control:push pop:editor:DeleteComment ?ASSURE 'Не удалось удалить комментарий'
 "Проверяем отсутствие комментария в текущем редакторе"
;

: "Проверяем наличие комментария в редакторе" OBJECT IN anEditor
 anEditor pop:editor:HasComment ?ASSURE 'Комментарий поставить не удалось'
;

: "Проверяем наличие комментария в текущем редакторе"
 "Убедиться, что фокус в редакторе"
 "Проверяем наличие комментария в редакторе {(focused:control:push)}"
;

: "Выделить таблицу в текущем редакторе"
 "Выделить таблицу в редакторе {(focused:control:push)}"
;

: "Выделить весь текст в текущем редакторе"
 "Убедиться, что фокус в редакторе"
 "Выделить всё в редакторе {(focused:control:push)}"
;

: "Установить в редакторе указатель мыши на точку для вызова КМ" OBJECT IN Editor INTEGER IN X0 INTEGER IN Y0 
 VAR x
 VAR y
 Editor pop:editor:CursorCoordsToScreen >>> y >>> x
 x X0 + >>> x
 y Y0 + >>> y
 x y Editor pop:Control:ScreenToClient
;

: "Скроллируем вниз раз" INTEGER IN aCount
 "Убедиться, что фокус в редакторе"
 OBJECT VAR l_Editor
 focused:control:push >>> l_Editor 
 aCount раз ( l_Editor pop:Editor:ScrollLineDown
 "Дать системе перерисоваться" )
;

: "Скроллируем вверх раз" INTEGER IN aCount
 "Убедиться, что фокус в редакторе"
 OBJECT VAR l_Editor
 focused:control:push >>> l_Editor 
 aCount раз ( l_Editor pop:Editor:ScrollLineUp
 "Дать системе перерисоваться" )
;

: "Выделить параграфов" INTEGER IN aCount
 "Убедиться, что фокус в редакторе"
 OBJECT VAR l_Editor
 focused:control:push >>> l_Editor 
 l_Editor pop:editor:ParaHome
 // - чтобы выделять с начала параграфа, иначе эталоны будут нестабильны
 cc:StartSelBlock
 "{(aCount)} раз {(@ "Перейти на параграф вниз")}"
 l_Editor pop:editor:ParaEnd
 // - чтобы выделять ДО конца параграфа, иначе эталоны будут нестабильны
 cc:EndSelBlock
;

WordAlias "Выделить параграф" "Выделить параграфов"
WordAlias "Выделить параграфа" "Выделить параграфов"

PROCEDURE "Выделить параграфов вверх" INTEGER IN aCount
 "Убедиться, что фокус в редакторе"
 OBJECT VAR l_Editor
 focused:control:push >>> l_Editor 
 l_Editor pop:editor:ParaHome
 // - чтобы выделять с начала параграфа, иначе эталоны будут нестабильны
 cc:StartSelBlock
 "{(aCount)} раз {(@ "Перейти на параграф вверх")}"
 l_Editor pop:editor:ParaEnd
 // - чтобы выделять ДО конца параграфа, иначе эталоны будут нестабильны
 cc:EndSelBlock
;

WordAlias "Выделить параграф вверх" "Выделить параграфов вверх"
WordAlias "Выделить параграфа вверх" "Выделить параграфов вверх"

: "Сравнить с эталоном следующий параграф"
 "Перейти на параграф вниз"
 "Выделить {(1)} параграфов"
 "Сравнить выделенный текст текущего редактора с эталоном"
; // "Сравнить с эталоном следующий параграф"

USES
 EVDSchema.script
;

PROCEDURE "Убедиться, что фокус в таблице"
 "Убедиться, что фокус в редакторе"
 INTERFACE VAR l_P
 l_P := ( focused:control:push pop:Editor:PushParaFromCursor )
 l_P := ( l_P Para:GetParent )
 l_P := ( l_P Para:GetParent )
 l_P := ( l_P Para:GetParent )
 evd::id_Table l_P Para:Type:Inherits ?ASSURE 'Курсор не в таблице'
; // "Убедиться, что фокус в таблице"

: "Вставить строку в текущую таблицу"
 "Убедиться, что фокус в таблице"
 "Вставить строку в таблицу"
 ; // "Вставить строку таблицы"

: "Текущий выделенный текст"
 focused:control:push pop:editor:CurrentText
; // "Текущий выделенный текст"

: "Сравнить с эталоном левый отступ до края формы текущего редактора"
 "Убедиться, что фокус в редакторе"
 "Контрол в фокусе" "Левый отступ контрола" .
; // "Сравнить с эталоном левый отступ до края формы текущего редактора"

: "Ищем строку в эталоне документа в формате" STRING IN aStr INTEGER IN aFormat
 STRING VAR Etalon
 //cc:Copy
 // - затычка, иначе не будет правильно имя формата в код формата преобразовываться
 aFormat "Контрол в фокусе" "Взять выделенный текст для сравнения в определенном формате"  >>> Etalon
 Если (  "Позиция строки {(aStr)} в эталоне {(Etalon)}" БОЛЬШЕ 0 ) то
  (  
   [ ' Строка: ' aStr ' найдена. ' ] strings:Cat . 
  )
 иначе
  (  
   [ ' Строка: ' aStr ' не найдена. ' ] strings:Cat . 
  )
; // "Ищем строку в эталоне документа"

: "Ищем строку в эталоне документа" STRING IN aStr
 "Ищем строку {(aStr)} в эталоне документа в формате {(CF_RTF)}"
; // "Ищем строку в эталоне документа"

: "Узнать номер текущего параграфа"
 "Убедиться, что фокус в редакторе"
 evd::ti_Handle "Контрол в фокусе" pop:Editor:PushParaFromCursor Para:IntA
; // "Узнать номер текущего параграфа"

: "Ищем строку в эталоне HTML" STRING IN aStr
 "Ищем строку {(aStr)} в эталоне документа в формате {(CF_HTML)}"
; // "Ищем строку в эталоне HTML"

: "Вставить текст из документа в текущий редактор"  STRING IN aName
 aName "Контрол в фокусе" "Вставить текст из документа"
; // "Вставить текст из документа в текущий редактор"

PROCEDURE "Перерисовать и сравнить содержимое текущего редактора с эталоном"
 "Убедиться, что фокус в редакторе"
 "Контрол в фокусе" "Перерисовать редактор и сохранить результат в файл с расширением .shapes"
;

PROCEDURE "Выделить вторую ячейку первой строки в текущей таблице"
 "Убедиться, что фокус в таблице"
 1 0 1 0 "Контрол в фокусе" "Выделить ячейки"
;

PROCEDURE "Выделить первую ячейку в таблице"
 "Убедиться, что фокус в таблице"
 0 0 0 1 "Контрол в фокусе" "Выделить ячейки"
;

PROCEDURE "Выделить в таблице ячейки первой строки: с первой до " INTEGER IN aNumCell
 "Убедиться, что фокус в таблице"
 0 0 aNumCell aNumCell "Контрол в фокусе" "Выделить ячейки"
;

PROCEDURE "Выделить столбец таблицы" INTEGER IN aNumColumn
 "Убедиться, что фокус в таблице"
 --! aNumColumn
 aNumColumn "Контрол в фокусе" pop:editor:SelectColumn
;

PROCEDURE "Выделить первые 2 ячейки в текущей таблице"
 "Убедиться, что фокус в таблице"
 0 0 1 0 focused:control:push "Выделить ячейки"
;

PROCEDURE "Убедиться, что фокус находится в комментарии"
 "Убедиться, что фокус в редакторе"
 "Контрол в фокусе" "Шрифт контрола"  "Цвет фона" РАВНО 14737407 ?ASSURE 'Фокус не в комментарии!'
;

PROCEDURE "Сравнить с эталоном номер текущего параграфа"
 'ParaID: ' "Узнать номер текущего параграфа" ToPrintable Cat .
;

PROCEDURE "Выключить показ блоков в тексте"
 Если ( "Контрол в фокусе" pop:Editor:ShowDocumentParts ) то 
  ( оп::Настройки_панели_меток_Показывать_блоки_в_тексте )
;
