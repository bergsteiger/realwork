USES
 axiom:QueryCard
 axiom:Editor
 axiom:Tests
 axiom:cc
 axiom:Wait
 axiom:Waited
 axiom:форма
 axiom:контрол
 axiom:TenQueryCard
 
 WordsTranslation.script
 Forms.script
 @\CommonSystem.script
 @\SysUtils.script
 @\Controls.script
 Controls.script
 @\Mouse.script
 @\ExCtrls.script
 @\EditorCommon.script
;

PROCEDURE "Перевести фокус в поле 'Название' в Прайме"
 "Убедиться, что фокус в редакторе"
 'AT_THEMES_NAME' focused:control:push pop:QueryCard:Attribute:SetFocus
  // ?ASSURE 'Не удалось поставить фокус в поле Название'
;

PROCEDURE "Установить фокус в КЗ"
 OBJECT VAR l_Form
 ANYUSERTYPE форма::QueryCard vcm:FindForm ?ASSURE 'Не найдена форма QueryCard'
 >>> l_Form
 l_Form .TenQueryCard.Editor pop:Control:SetFocus
  ?ASSURE 'Не удалось поставить фокус в КЗ'
; // "Установить фокус в КЗ"

PROCEDURE "Перевести фокус в поле 'Наименование обслуживающей организации' в Прайме"
 "Убедиться, что фокус в редакторе"
 'AT_DEALER_NAME' focused:control:push pop:QueryCard:Attribute:SetFocus
;

PROCEDURE "Перевести фокус в поле 'Вид информации' в Прайме"
 "Убедиться, что фокус в редакторе"
 'AT_ANNO_KIND' focused:control:push pop:QueryCard:Attribute:SetFocus
;

PROCEDURE "Перевести фокус в поле 'Ваша профессия' в Прайме"
 "Убедиться, что фокус в редакторе"
 'AT_ANNO_USER' focused:control:push pop:QueryCard:Attribute:SetFocus
;

PROCEDURE "Перевести фокус в поле 'Ваша организация' в Прайме"
 "Убедиться, что фокус в редакторе"
 'AT_ANNO_ORG' focused:control:push pop:QueryCard:Attribute:SetFocus
;

PROCEDURE "Перевести фокус в поле 'Сфера интересов' в Прайме"
 "Убедиться, что фокус в редакторе"
 'AT_ANNO_INTEREST' focused:control:push pop:QueryCard:Attribute:SetFocus
;

PROCEDURE "Перевести фокус в поле 'Самые значимые документы' в Прайме"
 "Убедиться, что фокус в редакторе"
 'AT_IMPORTANT_DOCUMENTS' focused:control:push pop:QueryCard:Attribute:SetFocus
;

PROCEDURE "Перевести фокус в поле 'E-mail рассылки' в Прайме"
 "Убедиться, что фокус в редакторе"
 'AT_EMAIL' focused:control:push pop:QueryCard:Attribute:SetFocus
;

: "Подготовить поля"
 "Нажать {('Ctrl+Del')}"
 "Перевести фокус в поле 'Наименование обслуживающей организации' в Прайме"
 "Выделить всё"
 "Ввести строку {('Контактная информация отсутствует')}"
; // "Подготовить поля"

USES
 ProjectSpecific.script
;

PROCEDURE "Приготовиться к выполнению следующего теста"
 "Установить окно оболочки на передний план"
 "Установить фокус в КЗ" 
 "Подготовить поля"
 "Перевести фокус в поле 'Название' в Прайме"
 // - http://mdp.garant.ru/pages/viewpage.action?pageId=357338241&focusedCommentId=433557567&#comment-433557567
;

INTEGER VAR g_EtalonCount
//[EXECUTE] 
( 0 >>> g_EtalonCount )

PROCEDURE "Сравнить текст редактора с эталоном" OBJECT IN anEditor
 ++! g_EtalonCount
 OnTest
 VAR l_File
 Если ( g_EtalonCount РАВНО 1 ) то
  ( script:FileName '.evd' sysutils:ChangeFileExt )
 иначе
  ( script:FileName '.' g_EtalonCount IntToStr Cat '.evd' Cat sysutils:ChangeFileExt )
 sysutils:ExtractFileName >>> l_File
 l_File anEditor pop:editor:TextToFile
 l_File '%' tests:CheckEtalon
; // "Сравнить текст редактора с эталоном"

: "Сравнить выделенный текст редактора с эталоном в формате" OBJECT IN anEditor STRING INTEGER IN aFormat
 cc:Copy
 // - затычка, иначе не будет правильно имя формата в код формата преобразовываться
 aFormat anEditor pop:editor:GetSelectionTextInFormat .
; // "Сравнить выделенный текст редактора с эталоном в формате"

PROCEDURE "Сравнить выделенный текст текущего редактора с эталоном в формате" STRING INTEGER IN aFormat
 "Убедиться, что фокус в редакторе"
 "Сравнить выделенный текст редактора {(focused:control:push)} с эталоном в формате {(aFormat)}"
; // "Сравнить выделенный текст текущего редактора с эталоном в формате"

PROCEDURE "Заполнить все обязательные поля Прайма"
 "Перевести фокус в поле 'Ваша профессия' в Прайме"
 "Ввести строку {('Бухгалтер, финансист')}"
 "Нажать {('Enter')}"
 "Перевести фокус в поле 'Ваша организация' в Прайме"
 "Ввести строку {('Бюджетная организация')}"
 "Нажать {('Enter')}"
 "Перевести фокус в поле 'Сфера интересов' в Прайме"
 "Ввести строку {('Банки, кредитные организации')}"
 "Нажать {('Enter')}"
; // "Заполнить все обязательные поля Прайма"

PROCEDURE "Нажать кнопку Сохранить в Прайме"
 OBJECT VAR "Сохранить"
 "Найти контрол {(контрол::bt_enResult_opOkExt)} на форме {("Главное окно")}" =: "Сохранить"
 "Сохранить" "Объект существует?" ?ASSURE 'Не нашли кнопку Сохранить'
 "Сохранить" "Кликнуть"
; // "Нажать кнопку Сохранить в Прайме"

PROCEDURE "Удалить сохраненную новостную ленту"
 "Найти контрол {(контрол::tvPostings)} на форме {("Главное окно")}" "Установить фокус"
 "Дождаться переключения вкладок"
 "Ответить один раз Да для {( @ "Удалить новостную ленту" )}"
 "Установить фокус в КЗ"
; // "Удалить сохраненную новостную ленту"

PROCEDURE "Проверить доступность кнопки" STRING IN aControl
 OBJECT VAR l_Control
 aControl "Главное окно" "Найти контрол по имени на форме" =: l_Control
 l_Control "Убедиться, что контрол активен" .
; // "Проверить доступность кнопки"

PROCEDURE "Проверить пункты командного меню " STRING IN aStr
 [ 'Элементы меню ' aStr ' : '   ] strings:Cat .
 aStr "Главное окно" "Найти элементы меню на форме " "Сравнить меню с эталоном"
 "Отделить текст в эталоне"
; // "Проверить пункты командного меню "

: "Проверить все пункты командного меню"
 "Очистить буфер обмена"
 "Проверить пункты командного меню {('Все')}"
; // "Проверить все пункты командного меню"

PROCEDURE "Сравнить содержимое блока Тематические параметры с эталоном"
 "Установить фокус в КЗ" 
 "Перевести фокус в поле 'Название' в Прайме"
 "Выделить всё"
 STRING VAR "Выделенный текст"
 'Поле КЗ: Название' .
 ( 'Текст: ' "Выделенный текст текущего редактора" ToPrintable Cat . )
 "Отделить текст в эталоне"
 "Перевести фокус в поле 'Самые значимые документы' в Прайме"
 "Выделить всё"
 'Поле КЗ: Самые значимые документы' .
 ( 'Текст: ' "Выделенный текст текущего редактора" ToPrintable Cat . )
 "Отделить текст в эталоне"
 "Выделенный текст текущего редактора" >>> "Выделенный текст"
 6 раз ( "Стрелка вниз" 
 Если ( "Узнать, в каком поле КЗ {("Контрол в фокусе")} стоит фокус" РАВНО 'AT_EMAIL' ! ) то 
   ( @ ( "Нажать {('Enter')}" ) MODAL ( 'Поле КЗ: ' "Контрол в фокусе" "Нижняя форма" "Заголовок контрола" ToPrintable Cat . )
     "Выделить всё"
     'Текст: ' "Выделенный текст текущего редактора" ToPrintable Cat .
     "Отделить текст в эталоне" 
   )
 )
; // "Сравнить содержимое блока Тематические параметры с эталоном"

: "Создать новостную ленту в Прайме, выполнить и удалить её"  IN aProc
 "Заполнить все обязательные поля Прайма"
 "Нажать кнопку Сохранить в Прайме"
 TRY
  aProc DO
 FINALLY
  "Удалить сохраненную новостную ленту"
 END
; // "Создать новостную ленту в Прайме, выполнить и удалить её"

: "Добавить ещё один атрибут с текстом" STRING IN aStr
 "Добавить ещё один атрибут"
 "Ввести строку {(aStr)}"
 "Нажать {('Enter')}"
 "Дождаться переключения вкладок"
; // "Добавить ещё один атрибут с текстом"

: "Сохранить новостную ленту, перезаписав старую"
 "Ответить один раз Да для {( @ "Нажать кнопку Сохранить в Прайме" )}"
; // "Сохранить новостную ленту, перезаписав старую"

PROCEDURE "Редактировать новостную ленту через контекстное меню на вкладке ПРАЙМ. Моя новостная лента"
 "Найти контрол {(контрол::tvPostings)} на форме {("Главное окно")}" "Установить фокус"
 "Редактировать индивидуальную ленту"
; // "Редактировать новостную ленту через контекстное меню на вкладке ПРАЙМ. Моя новостная лента"

CONST mbOk 1
CONST mbYes 2
CONST mbNo 3

: "И проверить наличие диалога с кнопкой" IN aProc INTEGER IN aButton
 aButton CASE 
  mbOk 
   wait:Ok
  mbYes 
   wait:Yes
  mbNo 
   wait:No
  DEFAULT 
   ERROR 'Неизвестная кнопка диалога'
 END
 TRY
  aProc DO
 FINALLY
  TRY
   waited:? ?ASSURE 'Не дождались диалога'
   // - убеждаемся, что диалог про конец поиска таки был
  EXCEPT
   "Сравнить текущее исключение с эталоном"
   DROP
   // - удаляем результат waited:? со стека, т.к. прилетела ересь 'Нарушен баланс скобок Wait'
   RAISE
  END 
 END
;

: "И проверить наличие диалога с кнопкой Да" IN aProc
 "{(aProc)} И проверить наличие диалога с кнопкой {(mbYes)}"
;

USES
 @\Windows.script
;

WORDWORKER "Подготовить главное окно к тесту (установить ему размеры ), ширину левого навигатора в и выполнить" INTEGER IN formW INTEGER IN formH INTEGER IN NavigatorW

   PROCEDURE Установили_размеры_окну
    "Установить ширину левого навигатора в {(NavigatorW)} и выполнить {(@  ( WordToWork DO ) )}"
   ; // Установили_размеры_окну

  PROCEDURE Действия
   "Выставить форме размеры {( formW formH )} и {(@ Установили_размеры_окну )}"
  ; 

 "Запомнить позицию мыши и выполнить" (
  "Сделать {(@ Действия )} с изменением состояния и размеров окна {("Главное окно")}"
 )
; // "Подготовить главное окно к тесту (установить ему размеры ) и выполнить"

PROCEDURE "Убедиться, что у текущего поля появился красный крестик 'Удалить/очистить строку ввода'"
 "Ставим указатель мыши на конец текущего параграфа редактора {("Контрол в фокусе")} со смещением {(475 -20)}"
 TRY
  "Дождаться появления хинта"
 FINALLY     
  "Удалить атрибут"
 END
;

WORDWORKER "Создать индивидуальную ленту, сохранить ее и выполнить"
 "Создать новостную ленту в Прайме, выполнить {(@  ( WordToWork DO ) )} и удалить её"
;

PROCEDURE "Добавить ещё один атрибут ('Единый налог на вмененный доход') в поле 'Сфера интересов'"
 "Добавить ещё один атрибут с текстом {('Единый налог на вмененный доход')}"
;

PROCEDURE "Редактировать новостную ленту через контекстное меню на вкладке и убедиться, что был диалог перезаписи. Сравнить с эталоном заполненные поля КЗ"
 TRY
  "{(@ "Редактировать новостную ленту через контекстное меню на вкладке ПРАЙМ. Моя новостная лента" )} И проверить наличие диалога с кнопкой Да"
 FINALLY
  "Сравнить содержимое блока Тематические параметры с эталоном"
 END 
;

PROCEDURE "Добавить ещё один атрибут - 'Специалист по кадрам'"
 "Добавить ещё один атрибут"
 "Ввести строку {('Специалист по кадрам')}"
 "Нажать {('Enter')}"
;

PROCEDURE "Сравнить с эталоном результат предварительного просмотра"
 TRY
  "Предварительный просмотр с эталонами"
 FINALLY 
 "Назад по истории"
 END
;

PROCEDURE "Переключить базу на" STRING IN aNewBase
 [ 
  'callbubat.cmd' 
  ( aNewBase "НЕ РАВНО" '' ) ? ( ' "' aNewBase '"' )
 ] strings:Cat WinExec
 2000 SLEEP
 OnTest
;

PROCEDURE "Переключаем базы"
 "Переключить базу на {('')}"
;

PROCEDURE "Выполнить с переключённой базой" IN aProc
 TRY
  "Переключаем базы"
  aProc DO
 FINALLY
  "Переключаем базы"
 END
;

WORDWORKER "Включить Предварительный просмотр и выполнить"
 TRY
  "Предварительный просмотр"
  ( WordToWork DO )
 FINALLY 
  "Назад по истории"
  "Дождаться переключения вкладок"
  "Вперёд по истории"
  "Дождаться переключения вкладок"
  "Назад по истории"
  "Дождаться переключения вкладок"
 END
;

