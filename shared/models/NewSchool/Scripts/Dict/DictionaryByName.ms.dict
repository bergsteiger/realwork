UNIT DictionaryByName.ms.dict

USES
 FirstElement.ms.dict
;

IMMEDIATE MACRO thisDictionary
 Ctx:Engine pop:ScriptEngine:CurrentCompilingDictionary CompileValue
; // thisDictionary

USES
 Chars.ms.dict
;

OBJECT FUNCTION .DictionaryByName
  IN aDictName
 nil >>> Result
 STRING VAR l_Code 
 [ 
  'INCLUDE ' 
  cQuote aDictName cQuote 
  ' [EXECUTE] ( Ctx:Engine pop:ScriptEngine:CurrentCompilingDictionary CompileValue ) ' 
 ] strings:Cat >>> l_Code 
 l_Code script:CompileStringAndProcess ( 
  DO
  VAR l_OurDict
  >>> l_OurDict
  
  l_OurDict pop:DictionaryEx:UsedDictionaries
  .filter> ( pop:DictionaryEx:FileName sysutils:ExtractFileName aDictName sysutils:ExtractFileName == )
  .FirstElement >>> Result
 ) // l_Code script:CompileStringAndProcess
; // .DictionaryByName

USES
 LoadOnDemand.ms.dict
;

: DictionaryAndMainWordByName
  IN aDictName
 VAR l_Dict
 aDictName .DictionaryByName >>> l_Dict
 VAR l_WordName
 aDictName sysutils:ExtractFileName >>> l_WordName
 '.' string:SplitTo! l_WordName >>> l_WordName
 'ME_' l_WordName Cat >>> l_WordName
 l_Dict
 WL l_WordName aDictName
; // DictionaryAndMainWordByName
