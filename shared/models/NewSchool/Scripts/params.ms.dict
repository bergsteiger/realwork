UNIT params.ms.dict

USES
 axiom_push.ms.dict
 macro.ms.dict
 Documentation.ms.dict
;

WordAlias RIGHT ^
 %REMARK 'Модификатор параметра справа'

WordAlias LVALUE_MOD ^@
 %REMARK 'Модификатор параметра слева, который передаётся по ссылке. И из которого можно не только читать, но и в который потенциально можно писать. Если он не Read-Only.'

WordAlias REF ^@
 %REMARK 'Ссылка на значение, из которого можно не только читать, но и писать'

WordAlias in IN
 %REMARK 'Описатель входного параметра (справа или слева)'

USES
 Literal.ms.dict
;

EXPORTS
 Literal.ms.dict

USES
 core.ms.dict
;

EXPORTS
 core.ms.dict

MACRO LVALUE
 Literal IN aName
 %SUMMARY 'Параметр слева, который передаётся по ссылке. И из которого можно не только читать, но и в который потенциально можно писать. Если он не Read-Only.' ;
 axiom:PushSymbol LVALUE_MOD
 axiom:PushSymbol IN
 aName |N Ctx:Parser:PushSymbol
; // LVALUE

PRIVATE VOID operator MangledNamePusher
 STRING IN aValueTypesName
 STRING IN aName
 %SUMMARY 'Реализация параметра справа' ;

 if ( aValueTypesName IsNil ! ) then
 begin
  aValueTypesName Ctx:Parser:PushSymbol
 end // ( aValueTypesName IsNil ! )

 axiom:PushSymbol (
 aName Ctx:Parser:PushSymbol
 axiom:PushSymbol |^
 axiom:PushSymbol )
; // MangledNamePusher

USES
 axiom:ValueTypes
;

MACRO right
 Literal IN aName
 %SUMMARY 'Параметр справа, который автоматически разыменовывается' ;

 TtfwValueTypes VAR l_ValueTypes
 Ctx:ValueTypes >>> l_ValueTypes
 Ctx:ClearTypeInfo

 STRING VAR l_ValueTypesName
 
 if ( l_ValueTypes IsNil ) then
 begin
  '' >>> l_ValueTypesName
 end // l_ValueTypes IsNil
 else
 begin
  l_ValueTypes pop:ValueTypes:Name >>> l_ValueTypesName
  l_ValueTypesName ' ' string:Pos -1 == ?ASSURE 'Множественные типы для параметров справа пока не поддерживаются'
 end // l_ValueTypes IsNil

 axiom:PushSymbol RIGHT
 axiom:PushSymbol IN

 STRING VAR l_Name
 aName |N >>> l_Name 

 STRING VAR l_MangledName
 [ '_' l_Name ] strings:Cat >>> l_MangledName

 l_MangledName Ctx:Parser:PushSymbol
 
  [
   'VOID'
   'IMMEDIATE axiom:operator' 
   l_Name
   l_ValueTypesName NameAsString
   l_MangledName NameAsString
   'MangledNamePusher'
   ';'
  ] Ctx:Parser:PushArray

; // right
